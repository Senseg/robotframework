*** Settings ***
Suite Setup     Set Log Level To DEBUG
Suite Teardown  Reset Log Level
Default Tags    python  ruby
Library         Remote  localhost:${PORT}
Variables       arguments.py

*** Variables ***
${PORT}  8270

*** Test Cases ***
One message Without Level
    [Documentation]  LOG 1 INFO Hello, world!
    One Message Without Level

Log Levels
    [Documentation]  LOG 1 DEBUG Debug message LOG 2 INFO Information message LOG 3 WARN Warning message
    Logging  Debug message  DEBUG
    Logging  Information message  INFO
    Logging  Warning message  WARN

Multiple Messages With Different Levels
    [Documentation]  LOG 1:1 INFO Info message LOG 1:2 DEBUG Debug message LOG 1:3 INFO Second info\n this time with two lines LOG 1:4 INFO Third info LOG 1:5 WARN Warning
    Multiple Messages With Different Levels

Log Unicode
    [Documentation]  LOG 1 INFO ${UNICODE}
    [Tags]  python
    Log Unicode

Logging And Failing
    [Documentation]  FAIL Too slow LOG 1:1 INFO This keyword will fail! LOG 1:2 WARN Run for your lives!! LOG 1:3 FAIL Too slow LOG 1:4 DEBUG REGEXP: Traceback.*
    Logging And Failing

Logging And Returning
    [Documentation]  LOG 1:1 INFO Logged message LOG 1:2 INFO \${ret} = Returned value LOG 3:1 WARN This keyword returns nothing LOG 3:2 INFO \${ret} =${SPACE}
    ${ret} =  Logging And Returning
    Should Be Equal  ${ret}  Returned value
    ${ret} =  Logging  This keyword returns nothing  WARN
    Should Be Equal  ${ret}  ${EMPTY}

Log Control Char
    [Documentation]  FAIL REGEXP: Processing XML-RPC return value failed. Most often this happens when the return value contains characters that are not valid in XML. Original error was: ExpatError: .*
    Log Control Char

*** Keywords ***
Set Log Level To DEBUG
    ${ORIG_LOG_LEVEL} =  Set Log Level  DEBUG
    Set Suite Variable  ${ORIG_LOG_LEVEL}

Reset Log Level
    Set Log Level  ${ORIG_LOG_LEVEL}

