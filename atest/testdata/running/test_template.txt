*** Settings ***
Documentation   NO RIDE because it would clean-up too much data.
Test Template  Should Be Equal

*** Variables ***
${VARIABLE}       Variable content
${SAME VARIABLE}  Variable content

*** Test Cases ***
Test Using Normal Keyword Is Not Possible With Template
  Fail  Fail

Test Default Template
  [Documentation]  FAIL Several failures occurred:\n\n1) Something != Different\n\n2) 42 != 43
  Same  Same
  Something  Different
  42  43

Test Overriding Default Template In Test
  [Documentation]  FAIL Same == Same
  [Template]  Should Not Be Equal
  Same  Same
  Something  Different
  42  43

Test Overriding Default Template In Test With Empty
  [documentation]  FAIL This should be executed as normal keyword
  [Template]
  Fail  This should be executed as normal keyword

Test Template With Variables
  [Template]  Expect Exactly Two Args
  ${VARIABLE}  ${VARIABLE}

Test Template With Variables And Keyword Name
  [template]  Expect Exactly Three Args
  ${SAME VARIABLE}  Variable content  ${VARIABLE}

Test Template With Variable And Assign Mark (=)
  [Documentation]  FAIL  1= != 2=
  [Template]  Expect Exactly Two Args
  ${42} =     42 =
  ${42}=      42=
  ${1}=       ${2}=

Test Empty Values
  [Template]  Expect Exactly Two Args
  \           \
  ${EMPTY}    \
    
Test Template With For
  [Documentation]  FAIL Several failures occurred:\n\n1) Templates not supported with FOR loops.\n\n2) Samething != Different
  Same  Same
  :FOR  ${i}  IN RANGE  10
  \  Log  ${i}
  Samething  Different

Test User Keywords Should Not Be Continued On Failure
    [Documentation]  FAIL Several failures occurred:\n\n1) expected failure\n\n2) second expected failure
    [Template]  Failing Uk With Multiple Fails
    expected failure
    second expected failure

Commented Rows With Test Template
    # My comment
    Same  Same  # Another comment
    # Yet another comment
    42  42

Templates with Run Keyword
    [Documentation]  FAIL  Several failures occurred:\n\n1) First failure\n\n2) No keyword with name 'Variable content =' found.
    [Template]  Run Keyword
    Should be equal  42  42
    Fail  First failure
    Expect exactly three args  xxx  xxx  xxx
    ${VARIABLE} =  Set variable  this doesn't work

Templates with continuable failures
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) Continuable 1\n\n
    ...  2) Continuable 1\n\n
    ...  3) Continuable 2\n\n
    ...  4) Continuable 1\n\n
    ...  5) Continuable 2\n\n
    ...  6) Continuable 3\n\n
    ...  7) Continuable 1\n\n
    ...  8) Continuable 2\n\n
    ...  9) Continuable 3\n\n
    ...  10) Continuable 4\n\n
    ...  11) Continuable 5
    [Template]  Continuable failures
    1
    2
    3
    5


*** Keywords ***
Expect Exactly Two Args  [arguments]  ${a1}  ${a2}
  Should Be Equal  ${a1}  ${a2}

Expect Exactly Three Args  [arguments]  ${a1}  ${a2}  ${a3}
  Should Be Equal  ${a1}  ${a2}
  Should Be Equal  ${a1}  ${a3}

Failing Uk With Multiple Fails  [arguments]  ${msg}
  Fail  ${msg}
  Fail  this should not occur

Continuable failures  [arguments]  ${count}
  :FOR  ${i}  IN RANGE  ${count}
  \  Run keyword and continue on failure  Fail  Continuable ${i+1}
