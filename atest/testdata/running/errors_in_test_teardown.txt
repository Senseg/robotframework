*** Settings ***
Library  Exceptions
Suite Teardown  Suite Teardown With Errors

*** Test Cases ***
One Error In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Message\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  One Error

Many Errors In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Several failures occurred:\n\n
    ...  1) Message 1\n\n
    ...  2) Message 2\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Many Errors

Errors In Teardown When Setting Variables
    [documentation]  FAIL  Teardown failed:\n
    ...  no return value is set\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Errors when setting variables

Errors In For Loop In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Several failures occurred:\n\n
    ...  1) cat\n\n
    ...  2) again\n\n
    ...  3) dog\n\n
    ...  4) again\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Errors In For Loop

Keyword Timeout In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Keyword timeout 42 milliseconds exceeded.\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Timeout

Fatal Error In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  FatalCatastrophyException\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Fatal Error


*** Keywords ***
One Error
    Fail  Message
    Log  This Should Be executed

Many Errors
    Fail  Message 1
    Fail  Message 2
    Log  This Should Also Be Executed

Errors when setting variables
    ${ret} =  Fail  no return value is set
    Should Be Equal  ${ret}  ${None}

Errors In For Loop
    :FOR  ${animal}  in  cat  dog
    \    Fail  ${animal}
    \    Fail  again

Fatal Error
    Exit On Failure
    Fail  This Should Not Be Executed

Timeout
    [timeout]  42 ms
    Sleep  1 s
    Fail  This Should Not Be Executed

Suite Teardown With Errors
    Fail  Suite Message 1
    Fail  Suite Message 2 (with ∏ön ÄßÇïï €§)
    Log  This As Well Should Be Executed