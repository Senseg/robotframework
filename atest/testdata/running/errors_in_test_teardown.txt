*** Settings ***
Library  Exceptions
Suite Teardown  Suite Teardown With Errors

*** Test Cases ***
Test With One Error In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Message\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  One Error

Test With Many Errors In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Several failures occurred:\n\n
    ...  1) Message 1\n\n
    ...  2) Message 2\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Many Errors

Test With Errors In For Loop In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  Several failures occurred:\n\n
    ...  1) cat\n\n
    ...  2) again\n\n
    ...  3) dog\n\n
    ...  4) again\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Errors In For Loop

Test With A Fatal Error In Teardown
    [documentation]  FAIL Teardown failed:\n
    ...  FatalCatastrophyException\n\n
    ...  Also teardown of the parent suite failed.
    No Operation
    [teardown]  Fatal Error

*** Keywords ***
One Error
    Fail  Message
    Log  This Should Be executed

Many Errors
    Fail  Message 1
    Fail  Message 2
    Log  This Should Also Be Executed

Errors In For Loop
    :FOR  ${animal}  in  cat  dog
    \    Fail  ${animal}
    \    Fail  again

Fatal Error
    Exit On Failure
    Log  This Should Not Be Executed

Suite Teardown With Errors
    Fail  Suite Message 1
    Fail  Suite Message 2 (with ∏ön ÄßÇïï €§)
    Log  This As Well Should Be Executed