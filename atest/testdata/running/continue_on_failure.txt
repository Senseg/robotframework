*** Settings ***
Suite Teardown  Continuable Failure In User Keyword In Suite Teardown   
Library  Exceptions


*** Variables ***
${ERROR}  ContinuableApocalypseException: Can be continued
${ERROR LINE}  ${ERROR}\n\n
${TEARDOWN ERROR}  Also teardown of the parent suite failed.
${ERROR LINE WITH TEARDOWN}  ${ERROR LINE}${TEARDOWN ERROR}


*** Test Cases ***
Continue in test
    [Documentation]  FAIL  ${ERROR LINE WITH TEARDOWN}
    Raise Continuable Failure
    Log  This should be executed

Continue in user keyword
    [Documentation]  FAIL  ${ERROR LINE WITH TEARDOWN}
    Continuable Failure In User Keyword In Test Case

Continue in test with several continuable failures
    [Documentation]   FAIL  Error 1: ${ERROR LINE}Error 2: ${ERROR LINE}Error 3: ${ERROR LINE WITH TEARDOWN}
    Raise Continuable Failure
    Log  This should be executed
    Raise Continuable Failure
    Log  This should also be executed
    Raise Continuable Failure
    Log  This too should also be executed

Continue in user keyword with several continuable failures
    [Documentation]   FAIL  Error 1: ${ERROR LINE}Error 2: ${ERROR LINE}Error 3: ${ERROR LINE WITH TEARDOWN}
    Several Continuable Failures In User Keyword In Test Case

Continuable and regular failure
    [Documentation]   FAIL  Error 1: ${ERROR LINE}Error 2: Stopping here!!\n\n${TEARDOWN ERROR}
    Raise Continuable Failure
    Log  This should be executed
    Fail  Stopping here!!
    Log  This should not be executed

Continue in nested user keyword
    [Documentation]  FAIL Error 1: ${ERROR} in top level\n\n
    ...  Error 2: ${ERROR}\n\n
    ...  Error 3: ${ERROR}\n\n
    ...  Error 4: ${ERROR}\n\n
    ...  Error 5: ${ERROR} in top level after nesting\n\n${TEARDOWN ERROR}
    Continuable Failure In Nested UK

Continuable and regular failure in nested UK
    [Documentation]   FAIL  Error 1: ${ERROR LINE}Error 2: Stop!!\n\n${TEARDOWN ERROR}
    Continuable and regular failure in nested UK
    Log  This should not be executed

Continue when setting variables
    [Documentation]   FAIL  Error 1: ${ERROR LINE}Error 2: ${ERROR LINE}Error 3: ${ERROR LINE WITH TEARDOWN}
    ${ret}=  Raise Continuable Failure
    Should Be Equal  ${ret}  ${None}
    ${r1}  ${r2}  ${r3}=  Raise Continuable Failure
    Should Be True  ${r1} == ${r2} == ${r3} == None
    @{list}=  Raise Continuable Failure
    Should Be True  @{list} == []

Continue in test setup
    [Documentation]  FAIL  Setup failed:\n${ERROR LINE WITH TEARDOWN}
    [setup]  Continuable Failure In User Keyword In Test Setup
    Log  This should not be executed

Continue in test teardown
    [Documentation]  FAIL  Teardown failed:\n${ERROR LINE WITH TEARDOWN}
    [Teardown]  Continuable Failure In User Keyword In Test Teardown
    No operation


*** User Keywords ***
Continuable Failure In User Keyword In ${where}
    Raise Continuable Failure
    Log  This should be executed in ${where}

Several Continuable Failures In User Keyword In ${where}
    Raise Continuable Failure
    Log  This should be executed in ${where}
    Raise Continuable Failure
    Log  This should also be executed in ${where}
    Raise Continuable Failure
    Log  This too should also be executed in ${where}

Continuable Failure In Nested UK
    Raise Continuable Failure  Can be continued in top level
    Log  This should be executed in Top Level UK
    Several Continuable Failures In User Keyword In Nested UK
    Raise Continuable Failure  Can be continued in top level after nesting

Continuable and regular failure in nested UK
    Continuable and regular failure
    Log  This should not be executed

Continuable and regular failure
    Raise Continuable Failure
    Log  This should be executed in Nested UK
    Fail  Stop!!