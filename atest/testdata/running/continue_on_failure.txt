*** Settings ***
Suite Teardown  Continuable Failure In User Keyword In Suite Teardown   
Library  Exceptions


*** Variables ***
${HEADER}  Several failures occurred:
${ERROR}  ContinuableApocalypseException: Can be continued
${TEARDOWN ERROR}  Also teardown of the parent suite failed.
${ERROR WITH TEARDOWN}  ${ERROR}\n\n${TEARDOWN ERROR}


*** Test Cases ***
Continue in test
    [Documentation]  FAIL  ${ERROR WITH TEARDOWN}
    Raise Continuable Failure
    Log  This should be executed

Continue in user keyword
    [Documentation]  FAIL  ${ERROR WITH TEARDOWN}
    Continuable Failure In User Keyword In Test Case

Continue in test with several continuable failures
    [Documentation]   FAIL  ${HEADER}\n\n
    ...  1) ${ERROR}\n\n
    ...  2) ${ERROR}\n\n
    ...  3) ${ERROR WITH TEARDOWN}
    Raise Continuable Failure
    Log  This should be executed
    Raise Continuable Failure
    Log  This should also be executed
    Raise Continuable Failure
    Log  This too should also be executed

Continue in user keyword with several continuable failures
    [Documentation]   FAIL  ${HEADER}\n\n
    ...  1) ${ERROR}\n\n
    ...  2) ${ERROR}\n\n
    ...  3) ${ERROR WITH TEARDOWN}
    Several Continuable Failures In User Keyword In Test Case

Continuable and regular failure
    [Documentation]   FAIL  ${HEADER}\n\n
    ...  1) ${ERROR}\n\n
    ...  2) Stopping here!! (with ∏ön ÄßÇïï €§)\n\n${TEARDOWN ERROR}
    Raise Continuable Failure
    Log  This should be executed
    Fail  Stopping here!! (with ∏ön ÄßÇïï €§)
    Fail  This should not be executed

Continue in nested user keyword
    [Documentation]  FAIL ${HEADER}\n\n
    ...  1) ${ERROR} in top level (with ∏ön ÄßÇïï €§)\n\n
    ...  2) ${ERROR}\n\n
    ...  3) ${ERROR}\n\n
    ...  4) ${ERROR}\n\n
    ...  5) ${ERROR} in top level after nesting (with ∏ön ÄßÇïï €§)\n\n${TEARDOWN ERROR}
    Continuable Failure In Nested UK

Continuable and regular failure in UK
    [Documentation]  FAIL ${HEADER}\n\n
    ...  1) ${ERROR}\n\n
    ...  2) Stop!!\n\n${TEARDOWN ERROR}
    Continuable and regular failure
    Fail  This should not be executed

Continuable and regular failure in nested UK
    [Documentation]  FAIL ${HEADER}\n\n
    ...  1) ${ERROR}\n\n
    ...  2)  Stop!!\n\n${TEARDOWN ERROR}
    Continuable and regular failure in nested UK
    Fail  This should not be executed

Continue when setting variables
    [Documentation]  FAIL ${HEADER}\n\n
    ...  1) ${ERROR}\n\n
    ...  2) ${ERROR}\n\n
    ...  3) ${ERROR}\n\n
    ...  4) No jokes\n\n${TEARDOWN ERROR}
    ${ret}=  Raise Continuable Failure
    Should Be Equal  ${ret}  ${None}
    ${r1}  ${r2}  ${r3}=  Raise Continuable Failure
    Should Be True  ${r1} == ${r2} == ${r3} == None
    @{list}=  Raise Continuable Failure
    Should Be True  @{list} == []
    ${notset} =  Fail  No jokes

Continue in test setup
    [Documentation]  FAIL  Setup failed:\n${ERROR WITH TEARDOWN}
    [setup]  Continuable Failure In User Keyword In Test Setup
    Fail  This should not be executed (with ∏ön ÄßÇïï €§)

Continue in test teardown
    [Documentation]  FAIL  Teardown failed:\n${ERROR WITH TEARDOWN}
    No operation
    [Teardown]  Continuable Failure In User Keyword In Test Teardown


*** User Keywords ***
Continuable Failure In User Keyword In ${where}
    Raise Continuable Failure
    Log  This should be executed in ${where}

Several Continuable Failures In User Keyword In ${where}
    Raise Continuable Failure
    Log  This should be executed in ${where} (with ∏ön ÄßÇïï €§)
    Raise Continuable Failure
    Log  This should also be executed in ${where}
    Raise Continuable Failure
    Log  This too should also be executed in ${where}

Continuable Failure In Nested UK
    Raise Continuable Failure  Can be continued in top level (with ∏ön ÄßÇïï €§)
    Log  This should be executed in Top Level UK (with ∏ön ÄßÇïï €§)
    Several Continuable Failures In User Keyword In Nested UK
    Raise Continuable Failure  Can be continued in top level after nesting (with ∏ön ÄßÇïï €§)

Continuable and regular failure in nested UK
    Continuable and regular failure
    Log  This should not be executed

Continuable and regular failure
    Raise Continuable Failure
    Log  This should be executed in Nested UK (with ∏ön ÄßÇïï €§)
    Fail  Stop!!
