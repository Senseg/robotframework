*** Settings ***
Suite Setup     Remove Files  @{SCREENSHOTS}
Test Setup      Save Start Time
Test Teardown   Remove Files  @{SCREENSHOTS}
Resource        screenshot_resource.txt
Library         Screenshot  ${OUTPUTDIR}  ${OUTPUTDIR}  WITH NAME  Screenshot2

*** Variables ***
@{SCREENSHOTS}  ${TEMPDIR}${/}screenshot_1.jpg  ${TEMPDIR}${/}screenshot_2.jpg  ${TEMPDIR}${/}mybase_1.jpg  ${CURDIR}${/}mybase_1.jpg  ${CURDIR}${/}screenshot.jpeg  ${TEMPDIR}${/}image_1.jpg  ${OUTPUTDIR}${/}image_1.jpg
...  ${OUTPUTDIR}${/}image_2.jpg  ${OUTPUTDIR}${/}screenshot_1.jpg  ${OUTPUTDIR}${/}screenshot_2.jpg  ${OUTPUTDIR}${/}screenshot_3.jpg

*** Test Cases ***
Save Screenshot To Default Dir Twice
    Screenshot.Save Screenshot
    Screenshot Should Exist  ${OUTPUTDIR}${/}screenshot_1.jpg
    Screenshot.Save Screenshot
    Screenshot Should Exist  ${OUTPUTDIR}${/}screenshot_2.jpg

Save Screenshot To Default Dir With File Name
    Screenshot.Save Screenshot  mybase
    Screenshot Should Exist  ${OUTPUTDIR}${/}mybase_1.jpg

Save Screenshot With File Name and Path Defined
    Screenshot.Save Screenshot  mybase  ${CURDIR}/./
    Screenshot Should Exist  ${CURDIR}/mybase_1.jpg

Save Screenshot To Non Existing Path
    [Documentation]  FAIL Directory '${CURDIR}${/}non-existing' where to save the screenshot does not exist
    Screenshot.Save Screenshot  mybase  ${CURDIR}/non-existing

Save Screenshot To Overwrite Should Succeed
    Screenshot.Save Screenshot To  ${CURDIR}/screenshot.jpeg
    Screenshot Should Exist  ${CURDIR}${/}screenshot.jpeg
    Save Start Time
    Screenshot.Save Screenshot To  ${CURDIR}${/}screenshot.jpeg
    Screenshot Should Exist  ${CURDIR}${/}screenshot.jpeg

Save Screenshot To With Non Existing Path
    [Documentation]  FAIL Directory '${CURDIR}${/}non-existing' where to save the screenshot does not exist
    Screenshot.Save Screenshot To  ${CURDIR}${/}non-existing${/}ne.jpg

Log Screenshot With Defaults
    Screenshot.Log Screenshot
    Screenshot Should Exist  ${OUTPUTDIR}${/}screenshot_1.jpg

Log Screenshot With Filename Defined
    Screenshot.Log Screenshot  image
    Screenshot Should Exist  ${OUTPUTDIR}${/}image_1.jpg

Log Screenshot With Filename And Path Defined
    Screenshot.Log Screenshot  image  ${OUTPUTDIR}
    Screenshot Should Exist  ${OUTPUTDIR}${/}image_1.jpg

Log Screenshot With Filename, Path And Width Defined
    Screenshot.Log Screenshot  image  ${OUTPUTDIR}  width=50%
    Screenshot Should Exist  ${OUTPUTDIR}${/}image_1.jpg

Set Screenshot Directories With Default Path Should Be Deprecated
    Screenshot.Set Screenshot Directories  ${OUTPUTDIR}
    Screenshot.Save Screenshot To  ${OUTPUTDIR}${/}screenshot_1.jpg
    Screenshot.Log Screenshot
    Screenshot Should Exist  ${OUTPUTDIR}${/}screenshot_2.jpg

Set Screenshot Directories With Default Path and Log Path Defined Should Be Deprecated
    Screenshot.Set Screenshot Directories  ${OUTPUTDIR.replace('\\', '/')}  ${OUTPUTDIR}/
    Screenshot.Log Screenshot
    Screenshot Should Exist  ${OUTPUTDIR}${/}screenshot_1.jpg

Log Screenshot With Initializing The Screenshot Library
    Screenshot2.Log Screenshot
    Screenshot Should Exist  ${OUTPUTDIR}${/}screenshot_1.jpg
