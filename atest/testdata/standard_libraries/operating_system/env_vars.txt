*** Setting ***
Suite Setup       Remove Environment Variable    ${NAME}
Test Teardown     Remove Environment Variable    ${NAME}
Library           OperatingSystem

*** Variable ***
${NAME}           EXAMPLE_ENV_VAR_32FDHT

*** Test Case ***
Get Environment Variable
    [Documentation]    FAIL Environment variable '${NAME}' does not exist
    ${var} =    Get Environment Variable    PATH
    Should Contain    ${var}    ${:}
    ${var} =    Get Environment Variable    ${NAME}    this is default
    Should Be Equal    ${var}    this is default
    ${var} =    Get Environment Variable    ${NAME}    # this keyword fails

Set Environment Variable
    Set Environment Variable    ${NAME}    Hello
    Should Be Equal    %{${NAME}}    Hello
    Set Environment Variable    ${NAME}    Moi
    Should Be Equal    %{${NAME}}    Moi

Remove Environment Variable
    [Documentation]    FAIL Environment variable '${NAME}' does not exist
    Set Environment Variable    ${NAME}    Hello
    Remove Environment Variable    ${NAME}
    Environment Variable Should Not Be Set    ${NAME}
    Remove Environment Variable    ${NAME}    # should not fail
    Get Environment Variable    ${NAME}

Environment Variable Should Be Set
    [Documentation]    FAIL Environment variable '${NAME}' is not set
    Set Environment Variable    ${NAME}    Hello
    Environment Variable Should Be Set    ${NAME}
    Remove Environment Variable    ${NAME}
    Environment Variable Should Be Set    ${NAME}

Environment Variable Should Be Set With Non Default Error
    [Documentation]    FAIL My error message
    Set Environment Variable    ${NAME}    Hello
    Environment Variable Should Be Set    ${NAME}    This does not fail
    Environment Variable Should Be Set    NON_EXISTING    My error message

Environment Variable Should Not Be Set
    [Documentation]    FAIL Environment variable '${NAME}' is set to 'Hello'
    Environment Variable Should Not Be Set    ${NAME}
    Set Environment Variable    ${NAME}    Hello
    Environment Variable Should Not Be Set    ${NAME}

Environment Variable Should Not Be Set With Non Default Error
    [Documentation]    FAIL My error message
    Environment Variable Should Not Be Set    ${NAME}    This does not fail
    Set Environment Variable    ${NAME}    Hello
    Environment Variable Should Not Be Set    ${NAME}    My error message

Set Environment Variable In One Test And Use In Another, Part 1
    [Documentation]    Environment variable is set in this test case and used in Part 2
    Set Environment Variable    ${NAME}    Hello another test case!
    [Teardown]

Set Environment Variable In One Test And Use In Another, Part 2
    [Documentation]    Environment variable was set in Part 1 and used here
    Should Be Equal    %{${NAME}}    Hello another test case!

Values are converted to strings automatically
    Set Environment Variable    ${NAME}    ${42}
    Should Be Equal    %{${NAME}}    42
    Set Environment Variable    ${NAME}    ${None}
    Should Be Equal    %{${NAME}}    None

