*** Settings ***
Suite Teardown  Remove Temps
Test Setup      Remove Temps
Library         OperatingSystem

*** Variables ***
${TESTDIR}  ${CURDIR}${/}robot_temp_dir

*** Test Cases ***

List Directory
    [Documentation]  This test also tests setting variables
    Create Dir  ${TESTDIR}
    Create File  ${TESTDIR}${/}foo.txt
    Create File  ${TESTDIR}${/}bar.txt
    ${files} =  List Directory  ${TESTDIR}
    Should Be True  ${files} == ['bar.txt','foo.txt']
    @{files2} =  List Directory  ${TESTDIR}
    Should Be Equal  @{files2}[0]  bar.txt
    Should Be Equal  @{files2}[1]  foo.txt
    ${file}  @{files3} =  List Dir  ${TESTDIR}
    Should Be Equal  ${file}  bar.txt
    Should Be Equal  @{files3}[0]  foo.txt
    Remove File  ${TESTDIR}${/}bar.txt
    ${files4} =  List Directory  ${TESTDIR}
    Should Be True  ${files4} == ['foo.txt']
    @{files5} =  List Directory  ${TESTDIR}
    Should Be Equal  @{files5}[0]  foo.txt
    ${file2}  @{files6} =  List Dir  ${TESTDIR}
    Should Be Equal  ${file2}  foo.txt
    Should Be True  @{files6} == []
    Remove File  ${TESTDIR}${/}foo.txt
    ${files7} =  List Directory  ${TESTDIR}
    Should Be True  ${files7} == []
    @{files8} =  List Directory  ${TESTDIR}
    Should Be True  @{files8} == []

List Directory With Patterns
    Comment  Setup
    Create Directory  ${TESTDIR}
    Create File  ${TESTDIR}${/}foo.txt
    Create File  ${TESTDIR}${/}bar.txt
    Create Directory  ${TESTDIR}${/}foodir
    Comment  Actual test
    ${names} =  List Directory  ${TESTDIR}  ???.txt
    Should Be True  ${names} == ['bar.txt', 'foo.txt']
    ${names} =  List Directory  ${TESTDIR}  *oo*
    Should Be True  ${names} == ['foo.txt', 'foodir']

List Directory With Absolute
    [Documentation]  This tests also List Files In Directory And List Directories In Directory
    Create Directory  ${TESTDIR}
    Create File  ${TESTDIR}${/}foo.txt
    Create File  ${TESTDIR}${/}bar.txt
    Create Directory  ${TESTDIR}${/}foodir
    ${names} =  List Directory  ${TESTDIR}  ${EMPTY}  absolute
    Log  ${names}
    Should Be Equal  ${names[0]}  ${TESTDIR}${/}bar.txt
    Should Be Equal  ${names[1]}  ${TESTDIR}${/}foo.txt
    Should Be Equal  ${names[2]}  ${TESTDIR}${/}foodir
    ${names} =  List Files In Directory  ${TESTDIR}  ${EMPTY}  whatever
    Should Be Equal  ${names[0]}  ${TESTDIR}${/}bar.txt
    Should Be Equal  ${names[1]}  ${TESTDIR}${/}foo.txt
    ${names} =  List Directories In Directory  ${TESTDIR}  ${EMPTY}  ${EMPTY}  deprecated absolute
    Should Be Equal  ${names[0]}  ${TESTDIR}${/}foodir

List And Count Files And Directories In Directory
    [Documentation]  Tests 'List Files In Directory', 'List Directories In Directory', 'Count Files In Directory', 'Count Directories In Directory' and 'Count Items In Directory'
    Comment  0) Setup
    Create Dir  ${TESTDIR}
    Create File  ${TESTDIR}${/}foo.txt
    Create File  ${TESTDIR}${/}bar.txt
    Create File  ${TESTDIR}${/}zap.txt
    Create Dir  ${TESTDIR}${/}foodir
    Create Dir  ${TESTDIR}${/}bardir
    Comment  1) List Files In Dir
    ${files} =  List Files In Directory  ${TESTDIR}
    ${bar_files} =  List Files In Directory  ${TESTDIR}  bar*
    Should Be True  ${files} == ['bar.txt','foo.txt','zap.txt']
    Should Be True  ${bar_files} == ['bar.txt']
    Comment  2) List Dirs In Dir
    ${dirs} =  List Directories In Directory  ${TESTDIR}
    ${foo_dirs} =  List Directories In Directory  ${TESTDIR}  *o*
    Should Be True  ${dirs} == ['bardir', 'foodir']
    Should Be True  ${foo_dirs} == ['foodir']
    ${file_count} =  Count Files In Directory  ${TESTDIR}
    ${txt_file_count} =  Count Files In Directory  ${TESTDIR}  *.txt
    Should Be Equal  ${file_count}  ${3}
    Should Be Equal  ${file_count}  ${txt_file_count}
    Comment  4) Count Dirs In Dir
    ${dir_count} =  Count Directories In Directory  ${TESTDIR}
    ${dir_dir_count} =  Count Directories In Directory  ${TESTDIR}  *dir
    Should Be Equal  ${dir_count}  ${2}
    Should Be Equal  ${dir_count}  ${dir_dir_count}
    Comment  5) Count Items In Dir
    ${count} =  Count Items In Directory  ${TESTDIR}
    ${foo_count} =  Count Items In Directory  ${TESTDIR}  foo*
    Should Be Equal  ${count}  ${5}
    Should Be Equal  ${foo_count}  ${2}
    Comment  6) Misc tests
    ${html_file_count} =  Count Files In Directory  ${CURDIR}  *.html
    ${html_dir_count} =  Count Directories In Directory  ${CURDIR}  *.html
    ${html_item_count} =  Count Items In Directory  ${CURDIR}  *.html
    Should Be True  ${html_file_count} > 0
    Should Be Equal  ${html_dir_count}  ${0}
    Should Be Equal  ${html_file_count}  ${html_item_count}


*** Keywords ***
Remove Temps
    Remove Dir  ${TESTDIR}  recursive
