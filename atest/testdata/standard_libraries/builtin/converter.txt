*** Settings ***
Variables       numbers_to_convert.py

*** Test Cases ***
Convert To Integer
    [Documentation]  FAIL STARTS: 'MyObject' cannot be converted to an integer: ZeroDivisionError:
    [Template]  Test Convert To Integer
    1  1
    -42  -42
    10000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000  10**100
    ${OBJECT}  42
    ${OBJECT_FAILING}  This fails!

Convert To Integer With Java Objects
    [Documentation]  FAIL STARTS: 'foobar' cannot be converted to an integer: ValueError:
    [Template]  Test Convert To Integer
    ${JAVA_STRING_INT}
    ${JAVA_INTEGER}
    ${JAVA_LONG}
    ${JAVA_SHORT}
    ${JAVA_FLOAT}
    ${JAVA_DOUBLE}
    ${JAVA_STRING_INVALID}  This fails!

Convert To Number
    [Documentation]  FAIL REGEXP: ^'MyObject' cannot be converted to a floating point number: (Attribute|Type)Error: .*$
    [Template]  Test Convert To Number
    -10.000    -10
    ${OBJECT}  42.0
    ${OBJECT_FAILING}  This fails!

Convert To Number With Java Objects
    [Documentation]  FAIL STARTS: 'foobar' cannot be converted to a floating point number: ValueError:
    [Template]  Test Convert To Number
    ${JAVA_STRING_INT}    1.0
    ${JAVA_STRING_FLOAT}  1.1
    ${JAVA_INTEGER}       1.0
    ${JAVA_LONG}          1.0
    ${JAVA_SHORT}         1.0
    ${JAVA_FLOAT}         1.1
    ${JAVA_DOUBLE}        1.1
    ${JAVA_STRING_INVALID}  This fails!

Convert To String
    ${int42} =  Convert To Integer  42
    Should Not Be Equal  ${int42}  42
    ${str42} =  Convert To String  ${int42}
    Should Be Equal  ${str42}  42

Convert To Boolean
    ${True} =  Convert To Boolean  True
    ${False} =  Convert To Boolean  false
    Should Be True  ${True} == True
    Should Be True  ${False} == False
    ${one} =  Convert To Integer  1
    ${zero} =  Convert To Integer  0
    ${True} =  Convert To Boolean  ${one}
    ${False} =  Convert To Boolean  ${zero}
    Should Be True  ${True} == True
    Should Be True  ${False} == False

Create List
    ${list} =  Create List  hello  world
    Should Be True  ${list} == ['hello','world']
    @{list} =  Create List  hello  world
    Should Be Equal  @{list}[0]  hello
    Should Be Equal  @{list}[1]  world
    ${one_item} =  Create List  one item
    Should Be True  ${one_item} == ['one item']
    ${empty} =  Create List
    Should Be True  ${empty} == [ ]
    ${int_one} =  Convert To Number  1
    ${mixed} =  Create List  one  ${int_one}
    Should Be True  ${mixed} == ['one', 1]

*** Keywords ***
Test Convert To Integer
    [Arguments]  ${item}  ${exp}=1
    ${act} =  Convert To Integer  ${item}
    ${exp} =  Evaluate  ${exp}
    Should Be Equal  ${act}  ${exp}

Test Convert To Number
    [Arguments]  ${item}  ${exp}=1.0
    ${act} =  Convert To Number  ${item}
    ${exp} =  Evaluate  ${exp}
    Should Be True  round(${act}, 6) == ${exp}
