*** Settings ***
Library  Exceptions

*** Test Cases ***
Run Keyword And Continue On Failure
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) Expected Failure\n\n
    ...  2) Expected Failure 2
    Run keyword And Continue On Failure  Fail  Expected Failure
    Run keyword And Continue On Failure  Fail  Expected Failure 2
    Log  This should be executed

Run Keyword And Continue On Failure In For Loop
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) 3 != 0\n\n
    ...  2) 3 != 1\n\n
    ...  3) 3 != 2\n\n
    ...  4) 3 != 4\n\n
    ...  5) Stop here!!
    :FOR  ${i}  IN RANGE  0  5
    \  Run keyword And Continue On Failure  Should Be Equal  ${3}  ${i}
    Fail  Stop here!!
    Fail  This isn't executed anymore

Run User keyword And Continue On Failure
    [Documentation]  FAIL  Expected Failure
    Run keyword And Continue On Failure  Exception In User Keyword
    Log  This should be executed

Run Keyword And Continue On Failure With Syntax Error
    [Documentation]  FAIL  Keyword 'BuiltIn.No Operation' expected 0 arguments, got 1.
    Run keyword And Continue On Failure  No Operation  illegal argument
    Fail  This Should Not Be Executed!

Run Keyword And Continue On Failure With Timeout
    [Documentation]  FAIL  Test timeout 100 milliseconds exceeded.
    [Timeout]  0.1 seconds
    Run keyword And Continue On Failure  Sleep  1 second
    Fail  This Should Not Be Executed!

Run Keyword And Continue On Failure With Fatal Error
    [Documentation]  FAIL  FatalCatastrophyException
    Run keyword And Continue On Failure  Exit On Failure
    Fail  This Should Not Be Executed!

This Should Fail With Fatal
    [Documentation]  FAIL  Test execution is stopped due to a fatal error
    No Operation

*** Keywords ***
Exception In User Keyword
    Fail  Expected Failure
