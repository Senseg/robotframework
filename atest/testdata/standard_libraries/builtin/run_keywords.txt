***Settings***
Suite Setup  Run keywords  Passing  ${NOOP}
Suite Teardown  Run keywords  Failing  Passing  Fail


***Variables***
${NOOP}      No Operation
${PASSING}   Passing
${FAILING}   Failing
@{KEYWORDS}  ${NOOP}  ${PASSING}  Log Variables
${TD ERR}    Teardown of the parent suite failed.
${ATD ERR}   \n\nAlso teardown of the parent suite failed.


***Test Cases***
Passing keywords
    [Documentation]  FAIL  ${TD ERR}
    Run keywords  No Operation  Passing  Log Variables

Failing keyword
    [Documentation]  FAIL  Expected error message${ATD ERR}
    Run keywords  Passing  Failing  Not Executed

Keywords as variables
    [Documentation]  FAIL  Expected error message${ATD ERR}
    Run keywords  ${NOOP}  ${PASSING}  @{KEYWORDS}  ${FAILING}

Non-existing variable as keyword name
    [Documentation]  FAIL  Non-existing variable '${NON-EXISTING}'.${ATD ERR}
    Run keywords  Not Executed  ${NON-EXISTING}  Not Executed

Non-existing keyword
    [Documentation]  FAIL  No keyword with name 'Non-Existing' found.${ATD ERR}
    Run keywords  Passing  Non-Existing  Non-Existing But Not Executed

Wrong number of arguments to keyword
    [Documentation]  FAIL  Keyword 'BuiltIn.Log' expected 1 to 2 arguments, got 0.${ATD ERR}
    Run keywords  Passing  Log  This isn't argument to Log keyword

In test setup
    [Documentation]  FAIL  Setup failed:\nExpected error message${ATD ERR}
    [Setup]  Run keywords  ${NOOP}  Passing  ${FAILING}  Not executed
    No Operation

In test teardown
    [Documentation]  FAIL  Teardown failed:\n
    ...  Several failures occurred:\n\n
    ...  1) Expected error message\n\n
    ...  2) AssertionError\n\n
    ...  3) No keyword with name 'Executed but doesn't exist' found.${ATD ERR}
    No Operation
    [Teardown]  Run keywords  Passing  ${NOOP}  Failing  ${NOOP}  Fail
    ...  Executed but doesn't exist
    ...  Not executed after syntax error


***Keywords***
Passing
    Log  Hello, world!

Failing
    Fail  Expected error message
