*** Settings ***
Suite Setup     Import Variables In Suite Setup
Library         OperatingSystem

*** Test Cases ***
Import Variables In Suite Setup
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  suite setup

Import Variables 1
    Variable Should Not Exist  $IMPORT_VARIABLES_1
    Import Variables  ${CURDIR}${/}variables_to_import_1.py
    Should Be Equal  ${IMPORT_VARIABLES_1}  Simple variable file

Import Variables 2
    [Documentation]  Verify that variable imported by earlier kw is still available
    Should Be Equal  ${IMPORT_VARIABLES_1}  Simple variable file

Import Variables With Arguments
    [Documentation]  FAIL REGEXP: Processing variable file '.*variables_to_import_2.py' with arguments \\[ 1 | 2 | 3\\] failed: TypeError: .*
    Import Variables  ${CURDIR}${/}variables_to_import_2.py  my  args
    Should Be Equal  ${IMPORT_VARIABLES_2}  Dynamic variable file
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  my args
    Import Variables  ${CURDIR}/variables_to_import_2.py  one arg only
    Should Be Equal  ${IMPORT_VARIABLES_2}  Dynamic variable file
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  one arg only default
    Comment  Copy to (hopefully) shorter path to prevent cutting error message
    Copy File  ${CURDIR}/variables_to_import_2.py  ${TEMPDIR}/variables_to_import_2.py
    Import Variables  ${TEMPDIR}/variables_to_import_2.py  1  2  3
    [Teardown]  Remove File  ${TEMPDIR}/variables_to_import_2.py

Import Variables In User Keyword 1
    Import Variables In User Keyword  Set in  user keyword
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  Set in user keyword
    Imported Variable Should Be Set To  Set in user keyword

Import Variables In User Keyword 2
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  Set in user keyword
    Imported Variable Should Be Set To  Set in user keyword
    Import Variables In User Keyword  Set again  user keyword
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  Set again in user keyword
    Imported Variable Should Be Set To  Set again in user keyword

Import Variables Failure Is Catchable
    Run Keyword And Expect Error  Variable file 'non_existing.py' does not exist.  Import Variables  non_existing.py

*** Keywords ***
Import Variables In User Keyword
    [Arguments]  @{value}
    Import Variables  ${CURDIR}${/}variables_to_import_2.py  @{value}
    ${value} =  Catenate  @{value}
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  ${value}
    Imported Variable Should Be Set To  ${value}

Imported Variable Should Be Set To
    [Arguments]  ${value}
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  ${value}

Import Variables In Suite Setup
    Import Variables In User Keyword  suite  setup
    Should Be Equal  ${IMPORT_VARIABLES_2_ARGS}  suite setup

