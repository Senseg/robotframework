*** Settings ***
Library  Collections
Resource  resource_for_get_variables.txt
Variables  vars_for_get_variables.py
Suite Setup  Set Some Variables

*** Variables ***
${MY VARIABLE}  dhrfad


*** Test cases ***
Automatic and Command Line Variables
  Variables Should Contain  \${/}  \${cmd line}

Variable Table
  Variables Should Contain  \${MY VARIABLE}

Global Variables
  Set Global In Resource
  Variables Should Contain  \${Global from suite setup}  \${GLOBAL FROM RESOURCE}

Suite Variables
  Set Suite Variable  ${Suite Var From Test}  Other value
  Variables Should Contain  \${Suite Var from suite setup}  \${Suite Var From Test}

Suite Variables 2
  Variables Should Contain  \${Suite Var from suite setup}  \${Suite Var From Test}

Resource File
  Variables Should Contain  \${RESOURCE VAR}

Variable File
  Variables Should Contain  \${var_in_variable_file}

Local Variables in Test Case Leak
  Variables Should Not Contain  \${local}
  ${local}=  Set Variable  lolcat
  Variables Should Contain  \${local}

Test Case Variable
  Set Test Variable  ${tc var}  tc
  Variables Should Contain  \${tc var}

Set Variable in User Keyword
  Set Var In UK

Get Length Of Variables
    ${variables}=   Get Variables
    ${var_count}  get length  ${variables}
    Should Be Equal As Integers  ${var_count}  34

* Keywords *
Set Some Variables
  Set Suite Variable  ${Suite Var from suite setup}  Some value
  Set Global Variable  ${Global from Suite setup}  Some value

Set Var In UK
  ${uk var}=  Set Variable  foo
  Variables Should Contain  \${uk var}

Variables Should Contain
  [Arguments]  @{keys}
  ${variables}=  Get Variables
  :FOR  ${key}  IN  @{keys}
  \  Dictionary Should Contain Key  ${variables}  ${key}

Variables Should Not Contain
  [Arguments]  @{keys}
  ${variables}=  Get Variables
  :FOR  ${key}  IN  @{keys}
  \  Dictionary Should Not Contain Key  ${variables}  ${key}
