*** Settings ***
Suite Setup  Import Resource  ${CURDIR}/import_resource_resource.txt

*** Test Cases ***
Import Resource In Suite Setup
    Variable Should Exist  ${VAR FROM IMPORT RESOURCE RESOURCE}
    Variable Should Exist  ${VAR FROM VARFILE 1}
    KW From Import Resource Resource

Import Resource With Sub Resources
    Variable Should Exist  ${VAR FROM IMPORT RESOURCE RESOURCE RESOURCE}
    KW From Import Resource Resource Resource
    Verify OperatingSystem Is Imported
    Variable Should Exist  ${VAR FROM VARFILE 2}

Import Resource In Test Case
    Import Resource  ${CURDIR}/import_resource_resource_2.txt
    Verify Test Case Resource Import
    Verify Test Case Resource Import In User Keyword

Import Resource In User Keyword
    Import Resource In User Keyword
    Verify User Keyword Resource Import
    Verify User Keyword Resource Import In User Keyword

Variables And Keywords Imported In Test Are Available In Next
    Verify Test Case Resource Import
    Verify User Keyword Resource Import

Import Resource Failure Is Catchable
    Run Keyword And Expect Error  Resource file 'non_existing.txt' does not exist.
    ...  Import Resource  non_existing.txt


*** User Keywords ***
Import Resource In User Keyword
    Import Resource  ${CURDIR}/import_resource_resource_3.txt
    Verify User Keyword Resource Import

Verify User Keyword Resource Import
    Variable Should Exist  ${VAR FROM IMPORT RESOURCE RESOURCE 3}
    KW From Import Resource Resource 3
    Verify String Is Imported

Verify User Keyword Resource Import In User Keyword
    Verify User Keyword Resource Import

Verify Test Case Resource Import
    Variable Should Exist  ${VAR FROM IMPORT RESOURCE RESOURCE 2}
    KW From Import Resource Resource 2
    Verify Collections Is Imported

Verify Test Case Resource Import In User Keyword
    Verify Test Case Resource Import

Verify OperatingSystem Is Imported
    Directory Should Exist  ${CURDIR}

Verify Collections Is Imported
    Count Values In List  ${TEST TAGS}  whatever

Verify String Is Imported
    Split To Lines  whatever
