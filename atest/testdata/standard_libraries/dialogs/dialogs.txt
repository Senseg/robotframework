*** Settings ***
Library         Dialogs
Test Timeout    1 minute

*** Test Cases ***
Pause Execution
    Pause Execution  Press OK.

Execute Manual Step
    [Documentation]  FAIL Predefined Error Message
    Execute Manual Step  Press PASS.
    Execute Manual Step  Press FAIL and OK.  Predefined Error Message

Get Value From User Passes
    ${value} =  Get Value From User  Input 'value' and press OK.
    Should Be Equal  ${value}  value

Get Value From User Fails
    [Documentation]  FAIL ValueError: No value provided by user
    Get Value From User  Press Cancel.

Get Selection From User Passes
    ${value} =  Get Selection From User  Select 'value' and press OK.  zip  zap  foo  value  bar
    Should Be Equal  ${value}  value

Get Selection From User Fails
    [Documentation]  FAIL ValueError: No value provided by user
    Get Selection From User  Press Cancel.  zip  zap  foo

Dialog and timeout
    [Documentation]  FAIL Test timeout 2 seconds exceeded.
    [Timeout]  2s
    Execute Manual Step  Press PASS (if you are fast enough)
    Execute Manual Step  Wait for timeout
