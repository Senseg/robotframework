*** Settings ***
Library  KwargsLibrary

*** Test Cases ***
Simple Kwarg
    Test With Kwargs

Substitution Starts From The End
    ${ret}=  Four Kwargs  a=foo  bar  d=quux
    Should Be Equal  ${ret}  a=foo, bar, None, quux

Mandatory Args Should Be Positioned
    ${ret}=  Mandatory And Kwargs  a  b  c
    Should Be Equal  ${ret}  a, b, c
    ${ret}=  Mandatory And Kwargs  a  c=b
    Should Be Equal  ${ret}  a, c=b, None

Kwargs and Varargs
    ${ret}=  Kwargs and Mandatory Args  mandatory  d2=d2  d1=d1
    Should Be Equal  ${ret}  mandatory, d1, d2, []
    ${ret}=  Kwargs and Mandatory Args  1  2  3  4  5  6
    Should Be Equal  ${ret}  1, 2, 3, [4, 5, 6]

Kwarg Syntax In Variable Is Ignored
    ${assignment}=  Set Variable  kwarg=value
    ${ret}=  One Kwarg  ${assignment}
    Should Be Equal  ${ret}  kwarg=value

Non-string value in kwarg
    ${ret}=  One Kwarg  kwarg=${42}
    Should Be Equal  ${ret}  ${42}

Equals Sign In Kwarg Value
    ${ret}=  One Kwarg  kwarg=bar=quux
    Should Be Equal  ${ret}  bar=quux
    ${ret}=  One Kwarg  kwarg===value
    Should Be Equal  ${ret}  ==value

Using non-existing kwarg
    ${ret}=  One Kwarg  notkwarg=value
    Should Be Equal  ${ret}  notkwarg=value
    ${ret}=  One Kwarg  ä=ö
    Should Be Equal  ${ret}  ä=ö

Escaping Kwarg
    ${ret}=  One Kwarg  kwarg=bar\\=quux
    Should Be Equal  ${ret}  bar\\=quux
    ${ret}=  One Kwarg  kwarg\\=bar=quux
    Should Be Equal  ${ret}  kwarg\\=bar=quux
    ${ret}=  One Kwarg  kwarg\=bar
    Should Be Equal  ${ret}  kwarg=bar

Using Argname Twice
    [documentation]  FAIL  Keyword argument fst repeated.
    ${ret}=  Two Kwargs  fst=foo  fst=foo

Inside Run Kw
    ${ret}=  Run Keyword  Four Kwargs  a=foo  bar  d=quux  
    Should Be Equal  ${ret}  a=foo, bar, None, quux

Simple Kwarg With Timeout
    [timeout]  10
    Test With Kwargs

*** Keywords ***
Test With Kwargs
    ${ret}=  One Kwarg  kwarg=bar
    Should Be Equal  ${ret}  bar
    ${ret}=  Two Kwargs  fst=bar  snd=quux
    Should Be Equal  ${ret}  bar, quux
    ${ret}=  Two Kwargs  snd=quux  fst=bar
    Should Be Equal  ${ret}  bar, quux