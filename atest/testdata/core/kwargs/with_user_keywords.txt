*** Test Cases ***
Simple Kwarg
    ${ret}=  One Kwarg  kwarg=quux
    Should Be Equal  ${ret}  quux
    ${ret}=  Two Kwargs  first=foo  second=bar
    Should Be Equal  ${ret}  foo, bar
    ${ret}=  Two Kw Args  second=World!  first=Hello
    Should Be Equal  ${ret}  Hello, World!

Substitution Starts From The End
    ${ret}=  Four Kwargs  a=foo  bar  d=quux
    Should Be Equal  ${ret}  a=foo, bar, default, quux

Equals Sign In Kwarg Value
    ${ret}=  One Kwarg  kwarg=bar=quux
    Should Be Equal  ${ret}  bar=quux

Using non-existing kwarg
    ${ret}=  One Kwarg  notkwarg=some value
    Should Be Equal  ${ret}  notkwarg=some value

Escaping Kwarg
    ${ret}=  One Kwarg  kwarg=bar\\=quux
    Should Be Equal  ${ret}  bar\\=quux
    ${ret}=  One Kwarg  kwarg\\=bar
    Should Be Equal  ${ret}  kwarg=bar

Mandatory Args Should Be Positioned
    ${ret}=  Mandatory And Kwargs  a  b  c
    Should Be Equal  ${ret}  a, b, c
    ${ret}=  Mandatory And Kwargs   a  c=b
    Should Be Equal  ${ret}  a, c=b, KWARG VALUE

Inside Run Kw
    ${ret}=  Run Keyword  Four Kwargs  a=foo  bar  d=quux
    Should Be Equal  ${ret}  a=foo, bar, default, quux

*** Keywords ***
One Kwarg
    [arguments]  ${kwarg}=
    [return]  ${kwarg}

Two Kwargs
    [arguments]  ${first}=  ${second}=
    [return]  ${first}, ${second}

Four Kw Args
    [arguments]  ${a}=default  ${b}=default  ${c}=default  ${d}=default
    [return]  ${a}, ${b}, ${c}, ${d}

Mandatory And Kwargs
    [arguments]  ${man1}  ${man2}  ${kwarg}=KWARG VALUE
    [return]  ${man1}, ${man2}, ${kwarg}