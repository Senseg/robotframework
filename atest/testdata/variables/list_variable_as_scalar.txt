*** Settings ***
Library         Collections

*** Variables ***
@{LIST}  spam  eggs  ${21}
@{SCALAR}  spam  eggs  ${21}

*** Test Cases ***
Using List Variable As Scalar
    Log  ${LIST}
    Should Be Equal  ${LIST}  ${SCALAR}
    Should Be True  @{LIST} == ${LIST}
    Length Should Be  ${LIST}  3

List Variable As Scalar With Extended Syntax
    Should Be Equal  ${LIST[0]} and ${LIST[1]}  spam and eggs
    Should Be Equal  ${list [2] * 2}  ${42}
    Should Be Equal  ${LIST.__len__()}  ${3}

Access and Modify List Variable With Keywords From Collections Library
    Lists Should Be Equal  ${LIST}  ${SCALAR}
    Append To List  ${LIST}  new value
    List Should Contain Value  ${LIST}  new value
    Remove Values From List  ${LIST}  eggs
    Set List Value  ${LIST}  0  ham
    Should Be True  ${LIST} == ['ham', 21, 'new value']
    Reverse List  ${LIST}
    Should Be Equal  @{LIST}[0]  new value
    Should Be Equal  @{LIST}[1]  ${21}
    Should Be Equal  @{LIST}[-1]  ham

Modifications To List Variables Live Between Test Cases
    Should Be True  ${LIST} == ['new value', 21, 'ham']

