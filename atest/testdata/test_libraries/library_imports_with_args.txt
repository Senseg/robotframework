*** Settings ***
Library    libswithargs.Mandatory  first arg       another arg

Library    libswithargs.Defaults   m1              WITH NAME  D1
Library    libswithargs.Defaults   m2  d1          WITH NAME  D2
Library    libswithargs.Defaults   m3  ${1}  ${2}  WITH NAME  D3

Library    libswithargs.Varargs    m1              WITH NAME  V1
Library    libswithargs.Varargs    m2  v1          WITH NAME  V2
Library    libswithargs.Varargs    m3  ${1}  ${2}  WITH NAME  V3

Library    libswithargs.Mixed      m1              WITH NAME  M1
Library    libswithargs.Mixed      m2  d1          WITH NAME  M2
Library    libswithargs.Mixed      m3  d2  v       WITH NAME  M3
Library    libswithargs.Mixed      m4  d3  v1  v2  WITH NAME  M4

Variables  ../../testresources/res_and_var_files/different_variables.py
Library    libswithargs.Mixed      ${LIST1}  ${DICTIONARY1}  ${None}  ${42}
Library    libswithargs.Defaults   @{LIST WITH OBJECTS}

Library    libswithargs.Mandatory  too few
Library    libswithargs.Defaults
Library    libswithargs.Varargs

Library    libswithargs.Mandatory  too  many  args  here
Library    libswithargs.Defaults   too  many  args  here  too

Library    libswithargs.Mandatory  ${NONEX}

Library    Collections


***Variables***
@{LIST WITH OBJECTS}  ${None}  ${1.0}  ${True}


*** Test Cases ***

Mandatory arguments
    Verify arguments  libswithargs.Mandatory  first arg  another arg

Default values
    Verify arguments  D1  m1  value  ${None}
    Verify arguments  D2  m2  d1     ${None}
    Verify arguments  D3  m3  ${1}   ${2}

Varargs
    Verify arguments  V1  m1  ${EMPTY}
    Verify arguments  V2  m2  v1
    Verify arguments  V3  m3  1 2

Mixed
    Verify arguments  M1  m1  ${42}  ${EMPTY}
    Verify arguments  M2  m2  d1     ${EMPTY}
    Verify arguments  M3  m3  d2     v
    Verify arguments  M4  m4  d3     v1 v2

Variables containing objects
    Verify arguments  libswithargs.Mixed  ${LIST1}  ${DICTIONARY1}  None 42
    Verify arguments  libswithargs.Defaults  ${None}  ${1.0}  ${True}


***Keywords***

Verify arguments
    [Arguments]  ${lib}  @{expected args}
    ${actual args} =  Run Keyword  ${lib}.Get Args
    Lists should be equal  ${actual args}  ${expected args}
