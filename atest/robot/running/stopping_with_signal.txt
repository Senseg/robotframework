*** Settings ***
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt
Library         ProcessManager.py
Test Teardown   Log Info From Process And Cleanup
Suite Setup     Set Timestamp
Test Setup      Set Test Debug File


*** Test Cases ***
SIGINT Signal Should Stop Test Execution Gracefully
    Start And Send Signal  without_any_timeout.txt  One Signal  SIGINT  0s
    Process Output For Graceful Shutdown
    Check Test Cases Have Failed Correctly

SIGTERM Signal Should Stop Test Execution Gracefully
    [tags]  non windows
    Start And Send Signal  without_any_timeout.txt  One Signal  SIGTERM  0s
    Process Output For Graceful Shutdown
    Check Test Cases Have Failed Correctly

One Signal Should Stop Test Execution Gracefully When Run Keyword Is Used
    Start And Send Signal  run_keyword.txt  One Signal  SIGTERM  0s
    Process Output For Graceful Shutdown
    Check Test Cases Have Failed Correctly

One Signal Should Stop Test Execution Gracefully When Test Timeout Is Used
    Start And Send Signal  test_timeout.txt  One Signal  SIGTERM  0s
    Process Output For Graceful Shutdown
    Check Test Cases Have Failed Correctly

One Signal Should Stop Test Execution Gracefully When Keyword Timeout Is Used
    Start And Send Signal  keyword_timeout.txt  One Signal  SIGTERM  0s
    Process Output For Graceful Shutdown
    Check Test Cases Have Failed Correctly

Two SIGINT Signals Should Stop Test Execution Forcefully
    Start And Send Signal  without_any_timeout.txt  Two Signals  SIGINT  2s
    Check Tests Have Been Forced To Shutdown

Two SIGTERM Signals Should Stop Test Execution Forcefully
    [tags]  non windows
    Start And Send Signal  without_any_timeout.txt  Two Signals  SIGTERM  2s
    Check Tests Have Been Forced To Shutdown

Two Signals Should Stop Test Execution Forcefully When Run Keyword Is Used
    Start And Send Signal  run_keyword.txt  Two Signals  SIGINT  2s
    Check Tests Have Been Forced To Shutdown

Two Signals Should Stop Test Execution Forcefully When Test Timeout Is Used
    Start And Send Signal  test_timeout.txt  Two Signals  SIGINT  2s
    Check Tests Have Been Forced To Shutdown

Two Signals Should Stop Test Execution Forcefully When Keyword Timeout Is Used
    Start And Send Signal  keyword_timeout.txt  Two Signals  SIGINT  2s
    Check Tests Have Been Forced To Shutdown

One Signal Should Stop Test Execution Gracefully And Test Case And Suite Teardowns Should Be Run
    Start And Send Signal  with_teardown.txt  One Signal  SIGINT  0s
    Process Output For Graceful Shutdown
    Check Test Cases Have Failed Correctly
    ${tc} =  Get Test Case  Test
    Check Log Message  ${tc.teardown.msgs[0]}  Logging Test Case Teardown
    ${ts} =  Get Test Suite  With Teardown
    Check Log Message  ${ts.teardown.kws[0].msgs[0]}  Logging Suite Teardown

*** Key Words ***
Start And Send Signal
    # Timeouts and signals apparently don't work on Windows
    # [timeout]  2 minutes
    [arguments]  ${testcase file}  ${signal method}  ${signal}  ${teardown sleep}
    ${is_jython_in_use} =  Running On Jython  ${INTERPRETER}
    ${test signal file} =  catenate  SEPARATOR=_  robot_signal_test_started  ${INTERPRETER}  .txt
    Remove File  ${TEMPDIR}/${test signal file}
    Run Keyword unless  ${is_jython_in_use}  Start Running Robot  ${test signal file}  running/stopping_with_signal/${testcase file}  ${teardown sleep}
    Run Keyword if  ${is_jython_in_use}  Start Running jybot  ${test signal file}  running/stopping_with_signal/${testcase file}  ${teardown sleep}
    Wait Until Created  ${TEMPDIR}/${test signal file}  1 minutes
    Run Keyword  ${signal method}  ${signal}
    Wait Until Finished With Timeout

Wait Until Finished With Timeout
    [timeout]  1 minute
    wait until finished

Start Running Robot
    [arguments]  ${test signal file}  ${datasource}  ${teardown sleep}
    Start Run  ${INTERPRETER}  ${test signal file}  ${datasource}  ${teardown sleep}

Start Running Jybot
    [arguments]  ${test signal file}  ${datasource}  ${teardown sleep}
    ${jybot} =  Get Jython Path
    Start Run  ${jybot}  ${test signal file}  ${datasource}  ${teardown sleep}

Start Run
    [arguments]  ${runner}  ${test signal file}  ${datasource}  ${teardown sleep}
    Set Runners
    ${path to datasource} =  Set Variables And Get Datasources  ${datasource}
    ${path to runner} =  Join Path  ${ROBOTPATH}  runner.py
    @{command} =  Create List  ${runner}  ${path to runner}  --variable  TESTSIGNALFILE:${test signal file}  --output  ${OUTFILE}  --report  None  --log  None  --debugfile  ${TEST DEBUG FILE}  --variable  TEARDOWNSLEEP:${teardown sleep}  ${path to datasource}
    Log Many  @{command}
    ProcessManager.start process  @{command}

Check Test Cases Have Failed Correctly
    Check Test Case  Test  FAIL  Execution terminated by signal
    Check Test Case  Test2  FAIL  Test execution is stopped due to a fatal error

Check Tests Have Been Forced To Shutdown
    ${stderr string}  ProcessManager.Get Stderr
    Should Contain  ${stderr string}  Execution forcefully stopped

Process Output For Graceful Shutdown
    Wait Until Created  ${OUTFILE}
    Process Output  ${OUTFILE}

One Signal
    [arguments]  ${signal}
    send terminate  ${signal}

Two Signals
    [arguments]  ${signal}
    send terminate  ${signal}
    Busy Sleep  1
    send terminate  ${signal}

Set Timestamp
    ${tmp time} =  Get Time
    Set Suite Variable  ${TEST TIMESTAMP}  ${tmp time}

Set Test Debug File
    Set Test Variable  ${TEST DEBUG FILE}  ${TEMPDIR}/${TESTNAME}_${TEST TIMESTAMP}.txt

Log Info From Process And Cleanup
    Run Keyword If Test Failed  Log Stdout And Stderr
    Run Keyword If Test Passed  Remove File  ${TEST DEBUG FILE}