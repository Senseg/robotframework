*** Settings ***
Documentation   Testing reading and processing data from xml outputs generated by Robot or Rebot itself.
Default Tags    regression  pybot  jybot
Resource        atest_resource.txt

*** Test Cases ***
Test Case File Suite
    [Documentation]  Testing that output file created from simple test case file is correct.
    My Run Robot And Rebot  ${EMPTY}  misc/normal.html
    Should Be Equal  ${SUITE.name}  Normal
    Should Be Equal  ${SUITE.doc}  Normal test cases
    Should Be Equal  ${SUITE.metadata['Something']}  My Value
    Should Be True  ${SUITE.metadata} == {'Something' : 'My Value'}
    Check Normal Suite Defaults  ${SUITE}
    Should Be Equal  ${SUITE.get_full_message()}  2 critical tests, 2 passed, 0 failed\n 2 tests total, 2 passed, 0 failed
    Should Be Equal  ${SUITE.get_stat_message()}  2 critical tests, 2 passed, 0 failed\n 2 tests total, 2 passed, 0 failed
    ${exp} =  Catenate  SEPARATOR=  2&nbsp;critical&nbsp;tests,&nbsp;  2&nbsp;passed,&nbsp;  <span>0&nbsp;failed</span>  <br />  2&nbsp;tests&nbsp;total,&nbsp;
    ...  2&nbsp;passed,&nbsp;  <span>0&nbsp;failed</span>
    Should Be Equal  ${SUITE.get_full_message(html=True)}  ${exp}
    Check Suite Contains Tests  ${SUITE}  First One  Second One

Directory Suite
    [Documentation]  Testing suite created from a test suite directory. Also testing metadata from cli.
    My Run Robot And Rebot  --metadata x:http://x.fi --name My_Name --doc Something  misc${/}suites
    Should Be Equal  ${SUITE.name}  My Name
    Should Be Equal  ${SUITE.doc}  Something
    Should Be Equal  ${SUITE.metadata['x']}  http://x.fi
    Should Be True  ${SUITE.get_metadata(html=True)} == [('x', '<a href="http://x.fi">http://x.fi</a>')]
    Check Suite Got From misc/suites/ Directory

Created Minimal
    [Documentation]  Testing absolute minimal hand created suite without even any tests or subsuites
    Run Rebot  ${EMPTY}  rebot${/}created_minimal.xml
    Should Be Equal  ${SUITE.name}  My Name
    Should Be Equal  ${SUITE.status}  PASS
    Should Be True  ${SUITE.suites}+${SUITE.tests}==[]
    Check Minimal Suite Defaults  ${SUITE}

Created Normal
    [Documentation]  Testing minimal hand created suite with tests or subsuites
    Run Rebot  ${EMPTY}  rebot${/}created_normal.xml
    Check Names  ${SUITE}  Root
    Should Be True  ${SUITE.suites} == ['Sub 1','Sub 2']
    Check Names  ${SUITE.suites[0]}  Sub 1  Root.
    Check Names  ${SUITE.suites[1]}  Sub 2  Root.
    Check Minimal Suite Defaults  ${SUITE}
    Check Minimal Suite Defaults  ${SUITE.suites[0]}
    Check Minimal Suite Defaults  ${SUITE.suites[1]}
    Check Suite Contains Tests  ${SUITE}  Test 1.1  Test 1.2  Test 2.1
    Check Names  ${SUITE.suites[0].tests[0]}  Test 1.1  Root.Sub 1.
    Check Names  ${SUITE.suites[0].tests[1]}  Test 1.2  Root.Sub 1.
    Check Names  ${SUITE.suites[1].tests[0]}  Test 2.1  Root.Sub 2.

*** Keywords ***
My Run Robot And Rebot
    [Arguments]  ${params}  @{paths}
    Run Tests Without Processing Output  ${params}  @{paths}
    Run Rebot  ${EMPTY}  ${OUTFILE}

Check Normal Suite Defaults
    [Arguments]  ${mysuite}  ${message}=  ${tests}=[]  ${teardown}=None
    Log  ${mysuite.name}
    Check Suite Defaults  ${mysuite}  ${message}  ${tests}  ${teardown}
    Check Normal Suite Times  ${mysuite}

Check Minimal Suite Defaults
    [Arguments]  ${mysuite}  ${message}=
    Check Suite Defaults  ${mysuite}  ${message}
    Check Minimal Suite Times  ${mysuite}

Check Normal Suite Times
    [Arguments]  ${mysuite}
    Is Valid Timestamp  ${mysuite.starttime}
    Is Valid Timestamp  ${mysuite.endtime}
    Is Valid Elapsed Time  ${mysuite.elapsedtime}
    Should Be True  ${mysuite.elapsedtime} >= 1

Check Minimal Suite Times
    [Arguments]  ${mysuite}
    Should Be Equal  ${mysuite.starttime}  N/A
    Should Be Equal  ${mysuite.endtime}  N/A
    Should Be Equal  ${mysuite.elapsedtime}  ${0}

Check Suite Defaults
    [Arguments]  ${mysuite}  ${message}=  ${tests}=[]  ${teardown}=None
    Should Be Equal  ${mysuite.message}  ${message}
    Should Be True  ${mysuite.critical.tags} == []
    Should Be True  ${mysuite.critical.nons} == []
    Should Be Equal  ${mysuite.setup}  ${None}
    ${teardown_name} =  Convert To String  ${mysuite.teardown}
    Should Be Equal  ${teardown_name}  ${teardown}

Check Suite Got From Misc/suites/ Directory
    Check Normal Suite Defaults  ${SUITE}  ${EMPTY}  []  BuiltIn.Log
    Should Be Equal  ${SUITE.status}  FAIL
    Should Be True  ${SUITE.suites} == ['Fourth', 'Subsuites', 'Subsuites2', 'Tsuite1', 'Tsuite2', 'Tsuite3']
    Should Be True  ${SUITE.tests} == []
    Should Be True  ${SUITE.suites[1].suites} == ['Sub1', 'Sub2']
    Should Be True  ${SUITE.suites[0].suites} + ${SUITE.suites[1].suites[0].suites}+ ${SUITE.suites[1].suites[1].suites}+ ${SUITE.suites[2].suites[0].suites}+ ${SUITE.suites[3].suites} + ${SUITE.suites[4].suites} + ${SUITE.suites[5].suites}== []
    Check Suite Contains Tests  ${SUITE}  SubSuite1 First  SubSuite2 First  SubSuite3 First  SubSuite3 Second  Suite1 First  Suite1 Second
    ...  Third In Suite1  Suite2 First  Suite3 First  Suite4 First
    Check Normal Suite Defaults  ${SUITE.suites[0]}  ${EMPTY}  []  BuiltIn.Log
    Check Normal Suite Defaults  ${SUITE.suites[1]}
    Check Normal Suite Defaults  ${SUITE.suites[1].suites[0]}
    Check Normal Suite Defaults  ${SUITE.suites[1].suites[1]}
    Check Normal Suite Defaults  ${SUITE.suites[2].suites[0]}
    Check Normal Suite Defaults  ${SUITE.suites[3]}
    Check Normal Suite Defaults  ${SUITE.suites[4]}

