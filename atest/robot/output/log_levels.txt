*** Settings ***
Documentation   Tests for setting log level from command line with --loglevel option. Setting log level while executing tests (BuiltIn.Set Log Level) is tested with BuiltIn library keywords.
Default Tags    regression  pybot  jybot
Resource        ../../resources/resource.txt

*** Variables ***
${TESTDATA}  misc/pass_and_fail.html

*** Test Cases ***
No Log Level Given
    [Documentation]  Default level of INFO should be used
    Run Tests  ${EMPTY}  ${TESTDATA}
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[0]}  Hello says "Pass"!  INFO
    Should Be Equal As Integers  ${SUITE.tests[0].kws[0].kws[1].message_count}  0
    Check Log Message  ${SUITE.tests[1].kws[1].msgs[0]}  Expected failure  FAIL

Trace Level
    Run Tests  --loglevel TRACE  ${TESTDATA}
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[0]}  Arguments: [ Hello says "Pass"! | INFO ]  TRACE
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[1]}  Hello says "Pass"!  INFO
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[2]}  Return: None  TRACE
    Check Log Message  ${SUITE.tests[0].kws[0].kws[1].msgs[1]}  Debug message  DEBUG
    Check Log Message  ${SUITE.tests[1].kws[1].msgs[0]}  Arguments: [ msg=Expected failure ]  TRACE
    Check Log Message  ${SUITE.tests[1].kws[1].msgs[1]}  Expected failure  FAIL

Debug Level
    Run Tests  --loglevel debug  ${TESTDATA}
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[0]}  Hello says "Pass"!  INFO
    Check Log Message  ${SUITE.tests[0].kws[0].kws[1].msgs[0]}  Debug message  DEBUG
    Check Log Message  ${SUITE.tests[1].kws[1].msgs[0]}  Expected failure  FAIL

Info Level
    Run Tests  -L InFo  ${TESTDATA}
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[0]}  Hello says "Pass"!  INFO
    Should Be Equal As Integers  ${SUITE.tests[0].kws[0].kws[1].message_count}  0
    Check Log Message  ${SUITE.tests[1].kws[1].msgs[0]}  Expected failure  FAIL

Warn Level
    Run Tests  --loglevel WARN --variable LEVEL1:WARN --variable LEVEL2:INFO  ${TESTDATA}
    Check Log Message  ${SUITE.tests[0].kws[0].kws[0].msgs[0]}  Hello says "Pass"!  WARN
    Should Be Equal As Integers  ${SUITE.tests[0].kws[0].kws[1].message_count}  0
    Check Log Message  ${SUITE.tests[1].kws[1].msgs[0]}  Expected failure  FAIL

Warnings Should Be Written To Syslog
    Should Be Equal  ${PREV TEST NAME}  Warn Level
    Check Log Message  ${ERRORS.msgs[0]}  Hello says "Suite Setup"!  WARN
    Check Log Message  ${ERRORS.msgs[1]}  Hello says "Pass"!  WARN
    Check Log Message  ${ERRORS.msgs[2]}  Hello says "Fail"!  WARN
    Should Be True  ${ERRORS.msg_count} == 3
    Check Syslog Contains  | WARN \ |  Hello says "Suite Setup"!
    Check Syslog Contains  | WARN \ |  Hello says "Pass"!
    Check Syslog Contains  | WARN \ |  Hello says "Fail"!

None Level
    Run Tests  --loglevel NONE --variable LEVEL1:ERROR --variable LEVEL2:FAIL  ${TESTDATA}
    Should Be Equal As Integers  ${SUITE.tests[0].kws[0].kws[0].message_count}  0
    Should Be Equal As Integers  ${SUITE.tests[0].kws[0].kws[1].message_count}  0
    Should Be Equal As Integers  ${SUITE.tests[1].kws[1].message_count}  0

