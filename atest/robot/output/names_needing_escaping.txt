*** Settings ***
Documentation   Test case and user keyword names needing escaping because they contain 1) special characters in HTML (", <, ...) 2) special characters in Robot test data. In latter case special chars actually aren't that special because they aren't escaped.
Suite Setup     Run Tests And Store Log
Force Tags      regression  pybot  jybot  static_html_checks
Resource        atest_resource.txt

*** Test Cases ***
Quotes
    Check TC And UK Name  "Quotes"
    Check Name In Log  &quot;Quotes&quot;  "Quotes"

Tag
    Check TC And UK Name  Tag <pre>
    Check Name In Log  Tag &lt;pre&gt;

And
    Check TC And UK Name  And &
    Check Name In Log  And &amp;

Backslashes
    Check TC And UK Name  Backslashes \\ \\\\ \\\\\\ \\\\\\\\
    Check Name In Log  Backslashes \\ \\\\ \\\\\\ \\\\\\\\

Variable
    Check TC And UK Name  Variable \${var}
    Check Name In Log  Variable \${var}

Newline And Tab
    Check TC And UK Name  Newline \\n and Tab \\t
    Check Name In Log  Newline \\n and Tab \\t

*** Keywords ***
Run tests And Store Log
    Run Tests  --log mylogfile.html  output${/}names_needing_escaping.tsv
    ${LOG} =  Get File  ${OUTDIR}${/}mylogfile.html
    Set Suite Variable  $LOG

Check TC And UK Name
    [Arguments]  ${name}
    ${tc} =  Check Test Case  ${name}
    Should Be Equal  ${tc.name}  ${name}
    Should Be Equal  ${tc.kws[0].name}  ${name}

Check Name In Log
    [Arguments]  ${name_in_attrs}  ${name_in_text}=${None}
    ${name_in_text} =  Set Variable If  ${name_in_text}  ${name_in_text}  ${name_in_attrs}
    ${attrs} =  Catenate  class="name"  name="test_Names Needing Escaping.${name_in_attrs}"  title="Names Needing Escaping.${name_in_attrs}"
    ${exp} =  Set Variable  <a ${attrs}>${name_in_text}</a>
    Should Contain  ${LOG}  ${exp}

