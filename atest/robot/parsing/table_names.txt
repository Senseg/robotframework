*** Settings ***
Suite Setup     Run Tests  ${EMPTY}  parsing/table_names.html
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt

*** Test Cases ***
Setting Table
    [Documentation]  Check Setting table and its synonyms Settings and Metadata
    Should Start With  ${SUITE.doc}  Testing that different synonyms for table names work. In general  # The doc is so long that don't want to test it fully...
    Check Test Tags  Test Case  metadata  settings

Variable Table
    [Documentation]  Check Variable table and its synonym Variables
    Check First Log Entry  Test Case  'Variable' name works
    Check First Log Entry  Test Cases  'Variables' name works

Test Case Table
    [Documentation]  Check Test Case table and its synonym Test Cases
    Check Test Case  Test Case
    Check Test Case  Test Cases

Keyword Table
    [Documentation]  Check Keyword table and its synonyms Keywords, User Keyword and User Keywords
    ${tc} =  Check Test Case  Test Case
    Check Log Message  ${tc.kws[1].kws[0].kws[0].kws[0].kws[0].msgs[0]}  'User Keywords' was executed

*** Keywords ***
Check First Log Entry
    [Arguments]  ${test case name}  ${expected}
    ${tc} =  Check Test Case  ${test case name}
    Check Log Message  ${tc.kws[0].msgs[0]}  ${expected}

