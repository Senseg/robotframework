*** Settings ***
Force Tags        pybot    jybot   regression
Library           TidyLib.py    ${INTERPRETER}
Library           OperatingSystem
Resource          atest_resource.txt
Suite Setup       Create Directory     ${TIDYDIR}
Suite Teardown    Remove Directory     ${TIDYDIR}    recursive=True

*** Variables ***
${DATADIR}        ${CURDIR}/../../testdata/tidy
${TIDYDIR}        ${TEMPDIR}/tidy-test

*** Test cases ***
Tidying single test case file
    [Template]    Run tidy with golden file and check result
    ${EMPTY}    ${DATADIR}/golden.txt
    --use-pipes    ${DATADIR}/golden_pipes.txt
    --format tsv    ${DATADIR}/golden.tsv
    --format html    ${DATADIR}/golden.html

Tidying single resource case file
    [Template]    Run tidy with golden resource file and check result
    ${EMPTY}    ${DATADIR}/golden_resource.txt
    --use-pipes    ${DATADIR}/golden_pipes_resource.txt
    --format tsv    ${DATADIR}/golden_resource.tsv
    --format html    ${DATADIR}/golden_resource.html

Tidying single init file
    Run tidy and check result    ${DATADIR}/__init__.txt    ${EMPTY}    ${DATADIR}/__init__.txt

Tidying single file in place
    [Setup]    Copy File    ${DATADIR}/golden.txt    ${TIDYDIR}/golden.txt
    Run tidy and check result    ${TIDYDIR}/golden.txt    --inplace --use-pipes    ${DATADIR}/golden_pipes.txt
    Check file count    ${TIDYDIR}    *.txt    1
    [Teardown]    Empty Directory     ${TIDYDIR}

Tidying single file in place and change format
    [Setup]    Copy File    ${DATADIR}/golden.txt    ${TIDYDIR}/golden.txt
    Run tidy and check result    ${TIDYDIR}/golden.txt    --inplace --format html    ${DATADIR}/golden.html
    Check file count    ${TIDYDIR}    *.html    1
    Check file count    ${TIDYDIR}    *.txt    0
    [Teardown]    Empty Directory     ${TIDYDIR}

Tidying many files in place
    [Setup]    Copy Golden Files
    List Directory      ${TIDYDIR}
    Run tidy and return output   ${TIDYDIR}/golden*    --inplace --format html
    List Directory      ${TIDYDIR}
    Check file count    ${TIDYDIR}    *.html    2
    Check file count    ${TIDYDIR}    *.txt    0
    Check file count    ${TIDYDIR}    *.tsv    0
    [Teardown]    Empty Directory     ${TIDYDIR}

Tidying directory
    [Setup]    Copy Directory    ${DATADIR}/tests    ${TEMPDIR}/tests
    ${output_before}=    Run Robot Directly    ${DATADIR}/tests
    Run Tidy and return output    ${TEMPDIR}/tests    --recursive --format tsv
    Check file count    ${TEMPDIR}/tests    *.tsv    2
    Check file count    ${TEMPDIR}/tests    *.txt    0
    ${output_after}=    Run Robot Directly    ${TEMPDIR}/tests
    Should Be Equal    ${output_before}    ${output_after}
    [Teardown]    Remove Directory    ${TEMPDIR}/tests    recurisive=True

*** Keywords ***
Run tidy with golden file and check result
    [Arguments]    ${options}    ${expected_result_file}
    Run tidy and check result    ${DATADIR}/golden.txt    ${options}    ${expected_result_file}

Run tidy with golden resource file and check result
    [Arguments]    ${options}    ${expected_result_file}
    Run tidy and check result    ${DATADIR}/golden_resource.txt    ${options}    ${expected_result_file}

Check file count
    [Arguments]    ${directory}    ${pattern}    ${expected count}
    ${files}=    Count Files In Directory    ${directory}    ${pattern}
    Should Be Equal As Numbers    ${files}    ${expected count}

Copy Golden Files
     Copy File    ${DATADIR}/golden_pipes.txt    ${TIDYDIR}/
     Copy File    ${DATADIR}/golden.tsv    ${TIDYDIR}/
