*** Settings ***
Force Tags        pybot    jybot   regression
Library           OperatingSystem
Resource          tidy_resource.txt
Suite Setup       Create Directory     ${TIDYDIR}
Suite Teardown    Remove Directory     ${TIDYDIR}    recursive=True

*** Test cases ***
Tidying single test case file
    [Documentation]   Test tidying to different formats
    [Template]    Run tidy with golden file and check result
    ${EMPTY}    ${DATADIR}/golden.txt
    --usepipes    ${DATADIR}/golden_pipes.txt
    --format tsv    ${DATADIR}/golden.tsv
    --format html    ${DATADIR}/golden.html
    --spacecount 2    ${DATADIR}/golden_two_spaces.txt

Tidying single resource file
    [Template]    Run tidy with golden resource file and check result
    ${EMPTY}    ${DATADIR}/golden_resource.txt
    -p    ${DATADIR}/golden_pipes_resource.txt
    -f tsv    ${DATADIR}/golden_resource.tsv
    --FORMAT html    ${DATADIR}/golden_resource.html

Tidying single init file
    Run tidy and check result    ${EMPTY}    ${DATADIR}/__init__.txt    ${DATADIR}/__init__.txt

Tidying single file in place
    [Setup]    Copy File    ${DATADIR}/golden.txt    ${TIDYDIR}/golden.txt
    Run tidy and check result    --inplace --usepipes    ${TIDYDIR}/golden.txt    ${DATADIR}/golden_pipes.txt
    Check file count    ${TIDYDIR}    *.txt    1
    [Teardown]    Empty Directory     ${TIDYDIR}

Tidying single file in place and change format
    [Setup]    Copy File    ${DATADIR}/golden.txt    ${TIDYDIR}/golden.txt
    Run tidy and check result    -i -f html    ${TIDYDIR}/golden.txt    ${DATADIR}/golden.html
    Check file count    ${TIDYDIR}    *.html    1
    Check file count    ${TIDYDIR}    *.txt    0
    [Teardown]    Empty Directory     ${TIDYDIR}

Tidying many files in place
    [Setup]    Copy Golden Files
    List Directory      ${TIDYDIR}
    Run tidy and return output    --InPlace --ForMat html   ${TIDYDIR}/golden*
    List Directory      ${TIDYDIR}
    Check file count    ${TIDYDIR}    *.html    2
    Check file count    ${TIDYDIR}    *.txt    0
    Check file count    ${TIDYDIR}    *.tsv    0
    [Teardown]    Empty Directory     ${TIDYDIR}

Tidying directory
    [Setup]    Copy Directory    ${DATADIR}/tests    ${TEMPDIR}/tests
    ${output_before}=    Run Robot Directly    ${DATADIR}/tests
    Run Tidy and return output    --recursive --format tsv    ${TEMPDIR}/tests
    Check file count    ${TEMPDIR}/tests    *.tsv    2
    Check file count    ${TEMPDIR}/tests    *.txt    0
    ${output_after}=    Run Robot Directly    ${TEMPDIR}/tests
    Should Be Equal    ${output_before}    ${output_after}
    [Teardown]    Remove Directory    ${TEMPDIR}/tests    recursive=True

Custom headers are preserved and tables aligned accordingly
    Run tidy and check result    ${EMPTY}     ${DATADIR}/golden_with_headers.txt    ${DATADIR}/golden_with_headers.txt

Running Tidy as a script
    Run tidy as a script and check result    ${EMPTY}    ${DATADIR}/golden.txt    ${DATADIR}/golden.txt


*** Keywords ***
Copy Golden Files
     Copy File    ${DATADIR}/golden_pipes.txt    ${TIDYDIR}/
     Copy File    ${DATADIR}/golden.tsv    ${TIDYDIR}/
