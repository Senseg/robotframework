*** Settings ***
Suite Setup     Create Dir  ${OUTPUT DIRECTORY}
Suite Teardown  Remove Dir  ${OUTPUT DIRECTORY}
Test Teardown   Empty Dir  ${OUTPUT DIRECTORY}
Force Tags      regression  pybot  jybot  static_html_checks
Resource        atest_resource.txt

*** Variables ***
${OUTPUT DIRECTORY}  ${TEMPDIR}${/}robot-test-999666

*** Test Cases ***
Tag Links Without Patterns
    Run Tests  --tagstatlink SUB3:http://example.com:title --outputdir ${OUTPUT DIRECTORY} --report report.html  misc/suites
    Report Should Contain Taglink  sub3  http://example.com  title

Tag Links With Pattern
    Run Tests  --tagstatlink ?1:http://example.com/%1:Title --outputdir ${OUTPUT DIRECTORY} --report report.html  misc/suites
    Report Should Contain Taglink  d1  http://example.com/d  Title
    Report Should Contain Taglink  f1  http://example.com/f  Title
    Report Should Contain Taglink  t1  http://example.com/t  Title

Tag Links With Many Patterns
    Run Tests  --tagstatlink *?u?*:%2-%3-%2-%1-%4:Title --outputdir ${OUTPUT DIRECTORY} --report report.html  misc/suites
    Report Should Contain Taglink  sub3  s-b-s--3  Title

Tag Link Used Multiple Times
    ${mail_to} =  Catenate  SEPARATOR=  mailto:  %1@dot.com  # ?subject=%1AMPbody=%1 #This does not work on Jython in Windows
    Run Tests  --tagstatlink ?2:ftp://%1:My_Title --tagstatlink s???:${mail_to}:mail_to --outputdir ${OUTPUT DIRECTORY} --report report.html --escape amp:AMP  misc/suites
    Report Should Contain Taglink  d2  ftp://d  My Title
    Report Should Contain Taglink  t2  ftp://t  My Title
    Report Should Contain Taglink  sub3  ${mail_to.replace('%1', 'ub3'). replace('AMP', '&')}  mail to

Same Tag Matches Multiple Links
    Run Tests  --tagstatlink sub3:one:First_Link --tagstatlink *3:%1:Second_Link --tagstatlink ??b?:%1-%2:Third --outputdir ${OUTPUT DIRECTORY} --critical none --report report.html  misc/suites
    ${report} =  Get File  ${OUTPUT DIRECTORY}${/}report.html
    ${expected} =  Catenate  SEPARATOR=\n  <td class="col_stat_name">  <div class="stat_name"><a href="#tag_sub3">sub3</a></div>  <div class="tag_links">
    : FOR  ${link}  ${title}  IN  one  First Link  sub  Second Link
    ...  su-3  Third
    \  ${exp_link} =  Get Expected Link  ${link}  ${title}
    \  ${expected} =  Set  ${expected}\n${exp_link}
    ${expected} =  Set  ${expected}\n</div>\n</td>
    Contains  ${report}  ${expected}

*** Keywords ***
Report Should Contain Taglink
    [Arguments]  ${tag}  ${link}  ${title}
    List Directory  ${OUTPUT DIRECTORY}
    ${report} =  Get File  ${OUTPUT DIRECTORY}${/}report.html
    ${expected} =  Catenate  SEPARATOR=\n  <td class="col_stat_name">  <div class="stat_name"><a href="#tag_${tag}">${tag}</a></div>  <div class="tag_links">  <span>[<a href="${link}">${title}</a>]</span>  </div>
    ...  </td>
    Contains  ${report}  ${expected}

Get Expected Link
    [Arguments]  ${link}  ${title}
    [Return]  <span>[<a href="${link}">${title}</a>]</span>

