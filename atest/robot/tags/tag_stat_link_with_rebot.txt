*** Settings ***
Suite Setup     My Setup
Suite Teardown  Remove Temps
Test Teardown   Empty Dir  ${MYOUTDIR}
Force Tags      regression  pybot  jybot  static_html_checks
Resource        atest_resource.txt

*** Variables ***
${MYOUTDIR}  ${TEMPDIR}${/}robot-test-978675
${MYINPUT}  ${TEMPDIR}${/}robot-test-978675.xml

*** Test Cases ***
Tag Links Without Patterns
    Run Rebot Without Processing Output  --tagstatlink SUB3:http://example.com:title --outputdir ${MYOUTDIR} --report report.html  ${MYINPUT}
    Report Should Contain Taglink  sub3  http://example.com  title

Tag Links With Pattern
    Run Rebot Without Processing Output  --tagstatlink ?1:http://example.com/%1:Title --outputdir ${MYOUTDIR} --report report.html  ${MYINPUT}
    Report Should Contain Taglink  d1  http://example.com/d  Title
    Report Should Contain Taglink  f1  http://example.com/f  Title
    Report Should Contain Taglink  t1  http://example.com/t  Title

Tag Links With Many Patterns
    Run Rebot Without Processing Output  --tagstatlink *?u?*:%2-%3-%2-%1-%4:Title --outputdir ${MYOUTDIR} --report report.html  ${MYINPUT}
    Report Should Contain Taglink  sub3  s-b-s--3  Title

Tag Link Used Multiple Times
    ${mail_to} =  Catenate  SEPARATOR=  mailto:  %1@dot.com  # ?subject=%1AMPbody=%1 #This does not work on Jython in Windows
    Run Rebot Without Processing Output  --tagstatlink ?2:ftp://%1:My_Title --tagstatlink s???:${mail_to}:mail_to --outputdir ${MYOUTDIR} --report report.html --escape amp:AMP  ${MYINPUT}
    Report Should Contain Taglink  d2  ftp://d  My Title
    Report Should Contain Taglink  t2  ftp://t  My Title
    Report Should Contain Taglink  sub3  ${mail_to.replace('%1', 'ub3'). replace('AMP', '&')}  mail to

Same Tag Matches Multiple Links
    Run Rebot Without Processing Output  --tagstatlink sub3:one:First_Link --tagstatlink *3:%1:Second_Link --tagstatlink ??b?:%1-%2:Third --outputdir ${MYOUTDIR} --critical none --report report.html  ${MYINPUT}
    ${report} =  Get File  ${MYOUTDIR}${/}report.html
    ${expected} =  Catenate  SEPARATOR=\n  <td class="col_stat_name">  <div class="stat_name"><a href="#tag_sub3">sub3</a></div>  <div class="tag_links">
    : FOR  ${link}  ${title}  IN  one  First Link  sub  Second Link
    ...  su-3  Third
    \  ${exp_link} =  Get Expected Link  ${link}  ${title}
    \  ${expected} =  Set Variable  ${expected}\n${exp_link}
    Should Contain  ${report}  ${expected}\n</div>\n</td>

*** Keywords ***
My Setup
    Run Tests Without Processing Output  ${EMPTY}  misc${/}suites
    Move File  ${OUTFILE}  ${MYINPUT}
    Create Directory  ${MYOUTDIR}

Remove Temps
    Remove Directory  ${MYOUTDIR}  recursive
    Remove File  ${MYINPUT}

Report Should Contain Taglink
    [Arguments]  ${tag}  ${link}  ${title}
    ${report} =  Get File  ${MYOUTDIR}${/}report.html
    ${expected} =  Catenate  SEPARATOR=\n  <td class="col_stat_name">  <div class="stat_name"><a href="#tag_${tag}">${tag}</a></div>  <div class="tag_links">  <span>[<a href="${link}">${title}</a>]</span>  </div>
    ...  </td>
    Should Contain  ${report}  ${expected}

Get Expected Link
    [Arguments]  ${link}  ${title}
    [Return]  <span>[<a href="${link}">${title}</a>]</span>

