*** Settings ***
Suite Setup     Run Test To Create Output
Suite Teardown  Remove File  ${INPUT FILE}
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt
Test Template   Test Criticality

*** Variables ***
${INPUT FILE}  ${TEMPDIR}/rebot-test-input.xml

*** Test Cases ***
Neither Critical Nor Non-Critical
    ${EMPTY}                                          yes    yes

Critical
    --critical pass                                   yes    no

Critical Not Matching
    -c nomatch                                        no     no

Critical With Pattern
    --CritiCal fSTAR -c QUESTaSS                      yes    yes

Non-Critical
    --noncritical pass                                no     yes

Non-Critical Not Matching
    -n these -n dont -n match                         yes    yes

Non-Critical With Pattern
    --NonCritiCal fSTAR -n QUESTass                   no     no

Critical And Non-Critical Matching Different Test
    -c paSTARss -n faiQUEST -c nomatch                yes    no

Critical And Non-Critical Matching Same Test
    -c force -n fail -c a -c b -c c -n x -n y -n z    yes    no

Critical And Non-Critical Matching Same Tag
    --critical force --noncritical fSTAR              no     no

*** Keywords ***
Run Test To Create Output
    Run Tests  --critical pass --noncritical fail  misc/pass_and_fail.html
    Move File  ${OUT FILE}  ${INPUT FILE}

Test Criticality
    [Arguments]  ${options}  ${crit 1}  ${crit 2}
    Run Rebot  ${options} --escape star:STAR --escape quest:QUEST --report rebot-report.html  ${INPUT FILE}
    ${tests} =  Get Nodes  ${OUT FILE}  suite/test
    Test Status And Criticality Should Be  ${tests[0]}  PASS  ${crit 1}
    Test Status And Criticality Should Be  ${tests[1]}  FAIL  ${crit 2}

Test Status And Criticality Should Be
    [Arguments]  ${test}  ${status}  ${criticality}
    [Documentation]  Must check status and criticality directly from XML because re-processed model would not have same criticality
    Should Be Equal  ${test.children[-1].attrs['status']}  ${status}
    Should Be Equal  ${test.children[-1].attrs['critical']}  ${criticality}
