*** Settings ***
Suite Setup     Run Tests And Rebot
Force Tags      pybot  jybot  regression
Resource        atest_resource.txt
Test Template   Tag doc should be correct in output


*** Test Cases ***

Simple Tag Doc
    3    f1    Some documentation for rebot

Specify Tag Doc With Pattern
    1    d1    Doc for many rebot tags

Tag Doc With Multiple Matches
    2    d2    Doc for many rebot tags &amp; More doc

Tag Doc With Formatting
    4    t1    <a href="http://some.url">http://some.url</a> <b>bold</b>

Tag Doc For Combined Statistics
    0    DX    Doc for many rebot tags


*** Keywords ***
Run Tests And Rebot
    Run Tests Without Processing Output  ${EMPTY}  misc/normal.html
    Run Rebot  --tagdoc f1:Some_documentation_for_rebot --tagdoc t1:http://some.url_*bold* --tagdoc d?:Doc_for_many_rebot_tags --tagdoc D2:More_doc --tagstatcombine d*:DX  ${OUT_FILE}

Tag doc should be correct in output
    [Arguments]  ${index}  ${tag}  ${doc}
    ${stats} =  Get Tag Stat Nodes
    Should Be Equal  ${stats[${index}].text}  ${tag}
    Should Be Equal  ${stats[${index}].attrs['doc']}  ${doc}

