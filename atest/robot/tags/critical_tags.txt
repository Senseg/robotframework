*** Settings ***
Documentation   Setting critical and non-critical tags
Force Tags     regression  pybot  jybot
Resource        atest_resource.txt
Test Template   Test Criticality

*** Variables ***
${M_000_211}  0 critical tests, 0 passed, 0 failed\n 2 tests total, 1 passed, 1 failed
${M_211_211}  2 critical tests, 1 passed, 1 failed\n 2 tests total, 1 passed, 1 failed
${M_110_211}  1 critical test, 1 passed, 0 failed\n 2 tests total, 1 passed, 1 failed
${M_101_211}  1 critical test, 0 passed, 1 failed\n 2 tests total, 1 passed, 1 failed

*** Test Cases ***
Neither Critical Nor Non-Critical
    ${EMPTY}  FAIL  ${M_211_211}

Critical
    --critical pass  PASS  ${M_110_211}

Critical Not Matching
    -c nomatch  PASS  ${M_000_211}

Critical With Pattern
    --critical fSTAR -c QUESTaSS  FAIL  ${M_211_211}

Non-Critical
    --noncritical pass  FAIL  ${M_101_211}

Non-Critical Not Matching
    -n these -n dont -n match  FAIL  ${M_211_211}

Non-Critical With Pattern
    --NonCritical fSTAR -n QUESTass  PASS  ${M_000_211}

Critical And Non-Critical Matching Different Test
    --critical paSTARss --noncritical faiQUEST -c nomatch  PASS  ${M_110_211}

Critical And Non-Critical Matching Same Test
    --critical force --noncritical fail -c a -c b -c c -n x -n y -n z  PASS  ${M_110_211}

Critical And Non-Critical Matching Same Tag
    --critical force --noncritical fSTAR  PASS  ${M_000_211}

*** Keywords ***
Test Criticality
    [Arguments]  ${options}  ${exp status}  ${exp msg}
    Run Tests  ${options} --escape star:STAR --escape quest:QUEST  misc/pass_and_fail.html
    Suite Status And Message Should Be  ${exp status}  ${exp msg}

Suite Status And Message Should Be
    [Arguments]  ${status}  ${message}
    [Documentation]  Must check status and message from stdout because re-processed suite would not have same criticality as during executiong
    Check Stdout Contains  Pass And Fail :: Some tests here ${SPACE * 36} | ${status} |\n${message}

