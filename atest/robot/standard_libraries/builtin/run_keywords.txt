*** Settings ***
Suite Setup     Run Tests  ${EMPTY}  standard_libraries/builtin/run_keywords.txt
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt

*** Test Cases ***

Passing keywords
    ${tc} =  Test Should Have Correct Keywords  BuiltIn.No Operation  Passing  BuiltIn.Log Variables
    Check Log Message  ${tc.kws[0].kws[1].kws[0].msgs[0]}  Hello, world!

Failing keyword
    Test Should Have Correct Keywords  Passing  Failing

Keywords as variables
    Test Should Have Correct Keywords  BuiltIn.No Operation  Passing
    ...  BuiltIn.No Operation  Passing  BuiltIn.Log Variables  Failing

Non-existing variable as keyword name
    ${tc} =  Check Test Case  ${TESTNAME}
    Should Be Empty  ${tc.kws[0].kws}

Non-existing keyword
    Test Should Have Correct Keywords  Passing  Non-Existing

Wrong number of arguments to keyword
    Test Should Have Correct Keywords  Passing  BuiltIn.Log

In test setup
    Check Test Case  ${TESTNAME}

In test teardown
    Check Test Case  ${TESTNAME}

In suite setup
    Check Log Message  ${SUITE.setup.kws[0].kws[0].msgs[0]}  Hello, world!
    Should Have Correct Keywords  ${SUITE.setup}  Passing  BuiltIn.No Operation

In suite teardown
    Should Have Correct Keywords  ${SUITE.teardown}
    ...  Failing  Passing  BuiltIn.Fail
    Should Be Equal  ${SUITE.message}
    ...  Suite teardown failed:\nSeveral failures occurred:\n\n1) Expected error message\n\n2) AssertionError


***Keywords*** 
Test Should Have Correct Keywords  [Arguments]  @{expected}
    ${tc} =  Check Test Case  ${TESTNAME}
    Should Have Correct Keywords  ${tc.kws[0]}  @{expected}
    [Return]  ${tc}

Should Have Correct Keywords  [Arguments]  ${kw}  @{expected}
    ${exp} =  Catenate  SEPARATOR=', '  @{expected}
    Should Be Equal As Strings  ${kw.kws}  ['${exp}']
