*** Settings ***
Documentation   Tests for logging using stdout/stderr
Suite Setup     Run Tests  --loglevel DEBUG  test_libraries/logging.txt
Force Tags      regression
Default Tags    pybot  jybot
Resource        atest_resource.txt

*** Test Cases ***
Logging From Python Library Using Stdout And Stderr
    ${tc} =  Check testcase  ${TEST NAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  Hello from Python Library!
    Check Log Message  ${tc.kws[1].msgs[0]}  Hello to stderr from Python Library!
    Check Log Message  ${tc.kws[2].msgs[0]}  stdout: Hello!!
    Check Log Message  ${tc.kws[2].msgs[1]}  stderr: Hello!!
    Check Stderr Contains  Hello to stderr from Python Library!\nstderr: Hello!!

Logging HTML From Python
    ${tc} =  Check testcase  ${TEST NAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  <a href="http://www.google.com">Google</a>  HTML
    Check Log Message  ${tc.kws[1].msgs[0]}  <table border=1>\n<tr><td>0,0</td><td>0,1</td></tr>\n <tr><td>1,0</td><td>1,1</td></tr>\n</table>  HTML
    Check Log Message  ${tc.kws[1].msgs[1]}  This is html <hr>  HTML
    Check Log Message  ${tc.kws[1].msgs[2]}  This is not html <br>  INFO
    Check Log Message  ${tc.kws[2].msgs[0]}  <i>Hello, stderr!!</i>  HTML
    Check Stderr Contains  *HTML* <i>Hello, stderr!!</i>

Logging From Java Library Using Stdout And Stderr
    [Tags]  jybot
    ${tc} =  Check testcase  ${TEST NAME}
    Check Log Message  ${tc.setup.msgs[0]}  Hello\nworld\n!!
    Check Log Message  ${tc.kws[0].msgs[0]}  Hello from Java library!
    Check Log Message  ${tc.kws[1].msgs[0]}  Hello Java stderr!!
    Check Stderr Contains  Hello Java stderr!!

Logging with Levels From Java
    [Tags]  jybot
    ${tc} =  Check testcase  ${TEST NAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  This is debug  DEBUG
    Check Log Message  ${tc.kws[1].msgs[0]}  First msg\n2nd line of1st msg  INFO
    Check Log Message  ${tc.kws[1].msgs[1]}  2nd msg *INFO* Still 2nd  INFO
    Check Log Message  ${tc.kws[2].msgs[0]}  1st msg\n2nd line
    Check Log Message  ${tc.kws[2].msgs[1]}  Second msg\n*INVAL* Still 2nd  WARN
    Check Log Message  ${tc.kws[2].msgs[2]}  Now 3rd msg
    Check Log Message  ${tc.kws[3].msgs[0]}  Warning to stderr  WARN
    Check Log Message  ${ERRORS.msgs[0]}  Second msg\n*INVAL* Still 2nd  WARN
    Check Log Message  ${ERRORS.msgs[1]}  Warning to stderr  WARN
    Check Stderr Contains  [ WARN ] Second msg\n*INVAL* Still 2nd\n
    Check Stderr Contains  [ WARN ] Warning to stderr\n*WARN* Warning to stderr

Logging HTML From Java
    [Tags]  jybot
    ${tc} =  Check testcase  ${TEST NAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  <b>Hello, stdout!</b>  HTML
    Check Log Message  ${tc.kws[1].msgs[0]}  <b>Hello, stderr!</b>  HTML
    Check Stderr Contains  *HTML* <b>Hello, stderr!</b>
