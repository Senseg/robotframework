*** Settings ***
Suite Setup     Run Tests  ${EMPTY}  variables/same_basename_is_deprecated.txt
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt

*** Test Cases ***

Variable Table
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \@{TABLE}  \${TABLE}

Assign Variables
    ${tc} =  Check Test Case  ${TESTNAME}
    Verify deprecation warning  \${assign}  \@{assign}
    Check Log Message  ${tc.kws[1].messages[0]}   Using same base name with scalar and list variables is deprecated. Please change either '\${assign}' or '\@{assign}' before Robot Framework 2.6.  WARN

Set Test/Suite/Global Variable
    ${tc} =  Check Test Case  ${TESTNAME}
    Verify deprecation warning  \@{SET}  \${SET}
    Check Log Message  ${tc.kws[1].messages[0]}   Using same base name with scalar and list variables is deprecated. Please change either '\@{SET}' or '\${SET}' before Robot Framework 2.6.  WARN

Variable File
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \${VARFILE}  \@{VARFILE}

Combinations
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \@{table2}  \${table2}


*** Keywords ***

Verify deprecation warning
    [Arguments]  ${1st}  ${2nd}
    Check Stderr Contains  [ WARN ]
    ...  Using same base name with scalar and list variables is deprecated. 
    ...  Please change either '${1st}' or '${2nd}' before Robot Framework 2.6.

