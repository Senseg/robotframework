*** Settings ***
Suite Setup     Run Tests  ${EMPTY}  variables/same_basename_is_deprecated.txt
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt

*** Test Cases ***

Variable Table
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \@{TABLE}  \${TABLE}

Assign Variables
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \${assign}  \@{assign}

Set Test/Suite/Global Variable
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \@{SET}  \${SET}

Variable File
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \${VARFILE}  \@{VARFILE}

Combinations
    Check Test Case  ${TESTNAME}
    Verify deprecation warning  \@{table2}  \${table2}


*** Keywords ***

Verify deprecation warning
    [Arguments]  ${1st}  ${2nd}
    Check Stderr Contains  [ WARN ]
    ...  Using same base name with scalar and list variables is deprecated. 
    ...  Please change either '${1st}' or '${2nd}' before Robot Framework 2.6.

