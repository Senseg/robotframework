*** Settings ***
Suite Setup     My Setup
Suite Teardown  Remove FIles  ${TEMP FILE 1}  ${TEMP FILE 2}  ${TEMP FILE 3}  ${REPORT 1}  ${REPORT 2}
Default Tags    regression  pybot  jybot
Resource        ../../resources/rebot_resource.html

*** Variables ***
${TEMP FILE 1}  ${TEMPDIR}${/}rebot-test-1.xml
${TEMP FILE 2}  ${TEMPDIR}${/}rebot-test-2.xml
${TEMP FILE 3}  ${TEMPDIR}${/}rebot-test-3.xml
${REPORT 1}  ${TEMPDIR}${/}rebot-report-1.html
${REPORT 2}  ${TEMPDIR}${/}rebot-report-2.html
${REPORT 1 EXP}  <td class="col_status fail">FAIL</td>\n <td class="col_msg">2&nbsp;critical&nbsp;tests,&nbsp;1&nbsp;passed,&nbsp;<span class="fail">1&nbsp;failed</span><br />4&nbsp;tests&nbsp;total,&nbsp;3&nbsp;passed,&nbsp;<span class="fail">1&nbsp;failed</span></td>
${REPORT 2 EXP}  <td class="col_status fail">FAIL</td>\n <td class="col_msg">10&nbsp;critical&nbsp;tests,&nbsp;9&nbsp;passed,&nbsp;<span class="fail">1&nbsp;failed</span><br />10&nbsp;tests&nbsp;total,&nbsp;9&nbsp;passed,&nbsp;<span class="fail">1&nbsp;failed</span></td>
${SUITE1}  Set in My Setup by combining Pass And Fail and Normal without options.
${SUITE2}  As previous but with --name, --doc, etc. options
${SUITE3}  Combined from Pass And Fail, Normal and Times
${SUITE4}  Combined from SUITE2 (recombine) and Times
@{PASS_FAIL_TESTS}  Pass  Fail
@{NORMAL_TESTS}  First One  Second One
@{TIMES_TESTS}  Incl-1  Incl-12  Incl-123  Excl-1  Excl-12  Excl-123

*** Test Cases ***
Combining Two
    Should Be True  ${SUITE1.suites} == ['Pass And Fail', 'Normal']
    Should Be True  ${SUITE2.suites} == ['Pass And Fail', 'Normal']
    Check Suite Contains Tests  ${SUITE1}  @{PASS_FAIL_TESTS}  @{NORMAL_TESTS}
    Check Suite Contains Tests  ${SUITE1.suites[0]}  @{PASS_FAIL_TESTS}
    Check Suite Contains Tests  ${SUITE1.suites[1]}  @{NORMAL_TESTS}
    Check Suite Contains Tests  ${SUITE2}  @{PASS_FAIL_TESTS}  @{NORMAL_TESTS}
    Check Suite Contains Tests  ${SUITE2.suites[0]}  @{PASS_FAIL_TESTS}
    Check Suite Contains Tests  ${SUITE2.suites[1]}  @{NORMAL_TESTS}

Combining Three
    Should Be True  ${SUITE3.suites} == ['Pass And Fail', 'Normal', 'Times']
    Check Suite Contains Tests  ${SUITE3}  @{PASS_FAIL_TESTS}  @{NORMAL_TESTS}  @{TIMES_TESTS}
    Check Suite Contains Tests  ${SUITE3.suites[0]}  @{PASS_FAIL_TESTS}
    Check Suite Contains Tests  ${SUITE3.suites[1]}  @{NORMAL_TESTS}
    Check Suite Contains Tests  ${SUITE3.suites[2]}  @{TIMES_TESTS}

Recombining
    Should Be True  ${SUITE4.suites} == ['Times', 'New Name']
    Should Be True  ${SUITE4.suites[1].suites} == ['Pass And Fail', 'Normal']
    Check Suite Contains Tests  ${SUITE4}  @{TIMES_TESTS}  @{PASS_FAIL_TESTS}  @{NORMAL_TESTS}
    Check Suite Contains Tests  ${SUITE4.suites[0]}  @{TIMES_TESTS}
    Check Suite Contains Tests  ${SUITE4.suites[1]}  @{PASS_FAIL_TESTS}  @{NORMAL_TESTS}
    Check Suite Contains Tests  ${SUITE4.suites[1].suites[0]}  @{PASS_FAIL_TESTS}
    Check Suite Contains Tests  ${SUITE4.suites[1].suites[1]}  @{NORMAL_TESTS}

Default Suite Name When Combining Two
    Check Names  ${SUITE1}  Pass And Fail & Normal
    Check Names  ${SUITE1.suites[0]}  Pass And Fail  Pass And Fail & Normal.
    Check Names  ${SUITE1.suites[1]}  Normal  Pass And Fail & Normal.

Overridden Suite Name
    Check Names  ${SUITE2}  New Name
    Check Names  ${SUITE2.suites[0]}  Pass And Fail  New Name.
    Check Names  ${SUITE2.suites[1]}  Normal  New Name.

Default Suite Name When Combining Three
    Check Names  ${SUITE3}  Pass And Fail & Normal & Times
    Check Names  ${SUITE3.suites[0]}  Pass And Fail  Pass And Fail & Normal & Times.
    Check Names  ${SUITE3.suites[1]}  Normal  Pass And Fail & Normal & Times.
    Check Names  ${SUITE3.suites[2]}  Times  Pass And Fail & Normal & Times.

Default Suite Name When Recombining
    Check Names  ${SUITE4}  Times & New Name
    Check Names  ${SUITE4.suites[0]}  Times  Times & New Name.
    Check Names  ${SUITE4.suites[1]}  New Name  Times & New Name.
    Check Names  ${SUITE4.suites[1].suites[0]}  Pass And Fail  Times & New Name.New Name.
    Check Names  ${SUITE4.suites[1].suites[1]}  Normal  Times & New Name.New Name.

Suite Documemt
    Should Be Equal  ${SUITE1.doc}  \
    Should Be Equal  ${SUITE2.doc}  My fine doc
    Should Be Equal  ${SUITE4.doc}  \
    Should Be Equal  ${SUITE4.suites[1].doc}  My fine doc

Suite Metadata
    Should Be True  ${SUITE1.metadata} == {}
    Should Be Equal  ${SUITE2.metadata['Name']}  value
    Should Be Equal  ${SUITE2.metadata['Other Meta']}  Another value

Critical Tags Can Be Set From Command LIne
    ${report} =  Get File  ${REPORT 1}
    Should Contain  ${report}  ${REPORT 1 EXP}

Critical Tags From Earlier Combines Should Be Ignored
    ${report} =  Get File  ${REPORT 2}
    Should Contain  ${report}  ${REPORT 2 EXP}

Suite Times
    Should Be Equal  ${SUITE3.starttime}  N/A
    Should Be Equal  ${SUITE3.endtime}  N/A
    Is Valid Elapsed Time  ${SUITE3.elapsedtime}
    Should Be True  ${SUITE3.elapsedtime} == ${MILLIS1} + ${MILLIS2} + 9999
    Is Valid Timestamp  ${SUITE3.suites[0].starttime}
    Is Valid Timestamp  ${SUITE3.suites[0].endtime}
    Is Valid Elapsed Time  ${SUITE3.suites[0].elapsedtime}
    Should Be Equal  ${SUITE3.suites[0].elapsedtime}  ${MILLIS1}
    Is Valid Timestamp  ${SUITE3.suites[1].starttime}
    Is Valid Timestamp  ${SUITE3.suites[1].endtime}
    Is Valid Elapsed Time  ${SUITE3.suites[1].elapsedtime}
    Should Be Equal  ${SUITE3.suites[1].elapsedtime}  ${MILLIS2}
    Should Be Equal  ${SUITE3.suites[2].starttime}  20061227 11:59:59.000
    Should Be Equal  ${SUITE3.suites[2].endtime}  20061227 12:00:08.999
    Should Be Equal  ${SUITE3.suites[2].elapsedtime}  ${9999}

Suite Times In Recombine
    Should Be Equal  ${SUITE4.starttime}  N/A
    Should Be Equal  ${SUITE4.endtime}  N/A
    Should Be True  ${SUITE4.elapsedtime} == 9999 + ${MILLIS1} + ${MILLIS2}
    Should Be Equal  ${SUITE4.suites[0].starttime}  20061227 11:59:59.000
    Should Be Equal  ${SUITE4.suites[0].endtime}  20061227 12:00:08.999
    Should Be Equal  ${SUITE4.suites[0].elapsedtime}  ${9999}
    Should Be Equal  ${SUITE4.suites[1].starttime}  N/A
    Should Be Equal  ${SUITE4.suites[1].endtime}  N/A
    Is Valid Timestamp  ${SUITE4.suites[1].suites[0].starttime}
    Is Valid Timestamp  ${SUITE4.suites[1].suites[0].endtime}
    Is Valid Elapsed Time  ${SUITE4.suites[1].suites[0].elapsedtime}
    Should Be Equal  ${SUITE4.suites[1].suites[0].elapsedtime}  ${MILLIS1}
    Is Valid Timestamp  ${SUITE4.suites[1].suites[1].starttime}
    Is Valid Timestamp  ${SUITE4.suites[1].suites[1].endtime}
    Is Valid Elapsed Time  ${SUITE4.suites[1].suites[1].elapsedtime}
    Should Be Equal  ${SUITE4.suites[1].suites[1].elapsedtime}  ${MILLIS2}

Combined Suite Names Are Correct In Statistics Table
    ${report content} =  Get File  ${REPORT 1}
    Should Contain  ${report content}  <div class="stat_name"><a href="#suite_New Name" title="New Name">New Name</a></div>
    Should Contain  ${report content}  <div class="stat_name"><a href="#suite_New Name.Pass And Fail" title="New Name.Pass And Fail"><span class="parent_name">New Name . </span>Pass And Fail</a></div>

Wildcards
    Run Rebot  ${EMPTY}  ${TEMPDIR}${/}rebot-test-?.x*
    Should Be True  ${SUITE.suites} == ['Pass And Fail', 'Normal', 'New Name']


*** Keywords ***
My Setup
    Comment  1) Create first input for Rebot
    Create Output With Robot  ${TEMP FILE 1}  --critical pass  misc${/}pass_and_fail.html
    Set Suite Variable  $MILLIS1  ${ORIG_ELAPSED}
    Comment  2) Create second input for Rebot
    Create Output With Robot  ${TEMP FILE 2}  -c f1 -c nonex --noncritical notag  misc${/}normal.html
    Set Suite Variable  $MILLIS2  ${ORIG_ELAPSED}
    Comment  3) Combine w/o options
    Run Rebot  ${EMPTY}  ${TEMP FILE 1}  ${TEMP FILE 2}
    Set Suite Variable  $SUITE1  ${SUITE}
    Comment  4) Combine w/ options
    Run Rebot  --name New_Name --doc MySPfineSPdoc --metadata Name:value -M Other_Meta:AnotherSPvalue --critical force --escape space:SP --report ${REPORT 1}  ${TEMP FILE 1}  ${TEMP FILE 2}
    Set Suite Variable  $SUITE2  ${SUITE}
    Copy File  ${OUT FILE}  ${TEMP FILE 3}
    Comment  5) Combine with times.xml  which has known times
    Run Rebot  ${EMPTY}  ${TEMP FILE 1}  ${TEMP FILE 2}  rebot${/}times.xml
    Set Suite Variable  $SUITE3  ${SUITE}
    Comment  6) Recombine
    Run Rebot  --report ${REPORT 2}  rebot${/}times.xml  ${TEMP FILE 3}
    Set Suite Variable  $SUITE4  ${SUITE}
    Comment  7) Prevent accidental usage
    Set Suite Variable  $SUITE  ${None}

