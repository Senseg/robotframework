*** Settings ***
Suite Setup       Remove Wait Until Keyword Succeeds with Rebot
Force Tags        regression    pybot    jybot
Resource          remove_keywords_resource.txt

*** Test Cases ***
Last failing Step is not removed
    Check Number Of Keywords     Fail Until The End    1

Last passing Step is not removed
    Check Number Of Keywords    Passes before timeout    2

Steps containing warnings are not removed
    Check Number Of Keywords    Warnings    3
    Check Number Of Keywords    One Warning    2

Nested Wait Until keywords are removed
    ${tc}=    Check Test Case    Nested
    Length Should Be    ${tc.kws[0].kws}    1
    Length Should Be    ${tc.kws[0].kws[0].kws}    1

*** Keywords ***
Remove Wait Until Keyword Succeeds with Rebot
    Create Output With Robot    ${INPUTFILE}    ${EMPTY}    rebot/wait_until_keyword_succeeds.txt
    Run Rebot    --removekeywords wuKs    ${INPUTFILE}

Check Number Of Keywords
    [Arguments]    ${test name}    ${expected number}
    ${tc}=    Check Test Case    ${test name}
    Length Should Be    ${tc.kws[0].kws}    ${expected number}

