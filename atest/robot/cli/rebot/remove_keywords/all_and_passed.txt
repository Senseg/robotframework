*** Settings ***
Documentation     Testing ALL and PASSED modes with --RemoveKeywords option.
Suite Setup       Run Some Tests
Suite Teardown    Remove File    ${INPUTFILE}
Force Tags        regression    pybot    jybot
Resource          remove_keywords_resource.txt

*** Test Cases ***
Remove All
    [Setup]    Run Rebot and Set My Suite    --RemoveKeywords ALL    0
    Keyword Should Be Empty    ${MY SUITE.setup}    My Keyword    ['Suite Setup']
    Keyword Should Contain Removal Message    ${MY SUITE.setup}
    ${tc1}    ${tc2} =    Set Variable    ${MY SUITE.tests}
    Length Should Be    ${tc1.keywords}    1
    Keyword Should Be Empty    ${tc1.keywords[0]}    My Keyword    ['Pass']
    Length Should Be    ${tc2.keywords}    2
    Keyword Should Be Empty    ${tc2.keywords[0]}    My Keyword    ['Fail']
    Keyword Should Be Empty    ${tc2.keywords[1]}    BuiltIn.Fail    ['msg=Expected failure']
    Keyword Should Contain Removal Message    ${tc2.keywords[1]}   Fails the test immediately with the given (optional) message.\n\n

Remove Passed
    [Setup]    Run Rebot and Set My Suite    --removekeywords passed    0
    Keyword Should Not Be Empty    ${MY SUITE.setup}    My Keyword    ['Suite Setup']
    ${tc1}    ${tc2} =    Set Variable    ${MY SUITE.tests}
    Length Should Be    ${tc1.keywords}    1
    Keyword Should Be Empty    ${tc1.keywords[0]}    My Keyword    ['Pass']
    Keyword Should Contain Removal Message     ${tc1.keywords[0]}
    Length Should Be    ${tc2.keywords}    2
    Keyword Should Not Be Empty    ${tc2.keywords[0]}    My Keyword    ['Fail']
    Keyword Should Not Be Empty    ${tc2.keywords[1]}    BuiltIn.Fail    ['msg=Expected failure']

Keywords With Warnings Are Not Removed When Passed are Removed
    [Setup]    Run Rebot and Set My Suite    --removekeywords Passed    1
    Keyword Should Not Be Empty    ${MY SUITE.setup}    Warning in    ['suite setup']
    Keyword Should Not Be Empty    ${MY SUITE.teardown}    Warning in    ['suite teardown']
    ${tc1}    ${tc2}=    Set Variable    ${MY SUITE.tests}
    Length Should Be    ${tc1.kws}    1
    Length Should Be    ${tc2.kws}    1
    Keyword Should Not Be Empty    ${tc1.kws[0]}    Warning in    ['test case']
    Keyword Should Not Be Empty    ${tc1.kws[0].kws[0]}    BuiltIn.Log    ['Warning in \${where}', 'WARN']
    Keyword Should Be Empty    ${tc2.kws[0]}    BuiltIn.Log    ['No warnings here']

Keywords With Warnings Are Removed When All are Removed
    [Setup]    Run Rebot and Set My Suite    --removekeywords All    1
    Keyword Should Be Empty    ${MY SUITE.setup}    Warning in    ['suite setup']
    Keyword Should Be Empty    ${MY SUITE.teardown}    Warning in    ['suite teardown']
    ${tc1}    ${tc2}=    Set Variable    ${MY SUITE.tests}
    Length Should Be    ${tc1.kws}    1
    Length Should Be    ${tc2.kws}    1
    Keyword Should Be Empty    ${tc1.kws[0]}    Warning in    ['test case']
    Keyword Should Be Empty    ${tc2.kws[0]}    BuiltIn.Log    ['No warnings here']

*** Keywords ***
Run Some Tests
    Create Output With Robot    ${INPUTFILE}    ${EMPTY}    misc${/}pass_and_fail.html    misc${/}warnings_and_errors.txt

Run Rebot And Set My Suite
    [Arguments]    ${rebot params}    ${suite index}
    Run Rebot    ${rebot params}    ${INPUTFILE}
    Should Not Be Equal    ${SUITE}    ${None}    Errors in test execution
    Set Test Variable    ${MY SUITE}    ${SUITE.suites[${suite index}]}

Keyword Should Contain Removal Message
    [Arguments]    ${keyword}    ${doc}=${EMPTY}
    Should Be Equal  ${keyword.doc}  ${doc}_Keyword data removed using --RemoveKeywords option._

