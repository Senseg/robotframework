*** Settings ***
Test Setup      Empty Output Directory
Suite Setup     Set Runners
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt
Resource        rebot_cli_resource.txt

*** Test Cases ***
Help
    ${rc}  ${help} =  Run And Return Rc And Output  ${REBOT} --help 2>&1
    Should Be Equal  ${rc}  ${251}
    Log  ${help.replace(' ','_').replace('\\t','_'*8)}
    Should Start With  ${help}  Rebot -- Robot Framework Report and Log Generator\n \n Version: \
    Should End With  ${help}  \n\n# Running 'robot/rebot.py' directly and creating combined outputs.\n $ python /path/robot/rebot.py -N Project_X -l x.html -r x.html outputs/*.xml\n
    Should Not Contain  ${help}  ERROR
    @{lines} =  Evaluate  [ '%d\\t%s' % (len(line), line) for line in ${help.splitlines()} ]
    Log Many  @{lines}
    @{long} =  Evaluate  [ line for line in ${help.splitlines()} if len(line) - line.count('\\\\') >= 80 ]
    Log Many  @{long}
    Should Be True  len(@{long}) == 0  Too long (>= 80) help line(s)
    ${help2} =  Run  ${REBOT} -h 2>&1
    Should Be Equal  ${help}  ${help2}

Version
    ${rc}  ${output} =  Run And Return Rc And Output  ${REBOT} --version 2>&1
    Should Be Equal  ${rc}  ${251}
    Log  ${output}
    Should Match Regexp  ${output}  ^Rebot (2\\.\\d+(\\.\\d+)?( (alpha|beta|rc)\\d*)?|trunk 20\\d{6}) \\([PJ]ython 2\\.[\\d.]+.+ on .+\\)$
    Should Be True  len("${output}") < 80  Too long version line
