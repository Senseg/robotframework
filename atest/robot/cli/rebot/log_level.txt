*** Settings ***
Force Tags      regression  pybot  jybot
Resource        rebot_resource.txt


*** Test Cases ***

By default all messages are included
    Rebot and process output
    ${tc} =  Set Variable  ${SUITE.tests[0]}
    Check Log Message  ${tc.kws[0].msgs[0]}  Arguments: [ u'Test 1' ]  TRACE
    Check Log Message  ${tc.kws[0].msgs[1]}  Test 1  INFO
    Check Log Message  ${tc.kws[0].msgs[2]}  Return: None  TRACE
    Check Log Message  ${tc.kws[1].msgs[0]}  Arguments: [ u'Logging with debug level' | u'DEBUG' ]  TRACE
    Check Log Message  ${tc.kws[1].msgs[1]}  Logging with debug level  DEBUG
    Check Log Message  ${tc.kws[1].msgs[2]}  Return: None  TRACE

Levels below given level are ignored
    Rebot and process output  --loglevel debug
    ${tc} =  Set Variable  ${SUITE.tests[0]}
    Check Log Message  ${tc.kws[0].msgs[0]}  Test 1  INFO
    Check Log Message  ${tc.kws[1].msgs[0]}  Logging with debug level  DEBUG
    Rebot and process output  -L INFO
    ${tc} =  Set Variable  ${SUITE.tests[0]}
    Check Log Message  ${tc.kws[0].msgs[0]}  Test 1  INFO
    Should Be Empty  ${tc.kws[1].msgs}

All messages are ignored when NONE level is used
    Rebot and process output  --loglevel NONE
    ${tc} =  Set Variable  ${SUITE.tests[0]}
    Should Be Empty  ${tc.kws[0].msgs}
    Should Be Empty  ${tc.kws[1].msgs}
