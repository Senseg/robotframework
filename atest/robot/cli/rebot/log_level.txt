*** Settings ***
Force Tags      regression  pybot  jybot
Resource        rebot_cli_resource.txt


*** Test Cases ***

By default all messages are included
    ${tc} =  Rebot
    Check Log Message  ${tc.kws[0].msgs[0]}  Arguments: [ u'Test 1' ]  TRACE
    Check Log Message  ${tc.kws[0].msgs[1]}  Test 1  INFO
    Check Log Message  ${tc.kws[0].msgs[2]}  Return: None  TRACE
    Check Log Message  ${tc.kws[1].msgs[0]}  Arguments: [ u'Logging with debug level' | u'DEBUG' ]  TRACE
    Check Log Message  ${tc.kws[1].msgs[1]}  Logging with debug level  DEBUG
    Check Log Message  ${tc.kws[1].msgs[2]}  Return: None  TRACE

Levels below given level are ignored
    ${tc} =  Rebot  --loglevel debug
    Check Log Message  ${tc.kws[0].msgs[0]}  Test 1  INFO
    Check Log Message  ${tc.kws[1].msgs[0]}  Logging with debug level  DEBUG
    ${tc} =  Rebot  -L INFO
    Check Log Message  ${tc.kws[0].msgs[0]}  Test 1  INFO
    Should Be Empty  ${tc.kws[1].msgs}

All messages are ignored when NONE level is used
    ${tc} =  Rebot  --loglevel NONE
    Should Be Empty  ${tc.kws[0].msgs}
    Should Be Empty  ${tc.kws[1].msgs}


*** Keywords ***
Rebot
    [Arguments]  ${arguments}=${EMPTY}
    Run Rebot  ${arguments}  ${MYINPUT}
    [Return]  ${SUITE.tests[0]}
