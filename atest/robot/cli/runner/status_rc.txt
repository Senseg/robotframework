*** Settings ***
Suite Setup     Set Runners
Default Tags    regression  pybot  jybot
Resource        cli_resource.txt

*** Variables ***
${MISCDIR}   ${CURDIR}/../../../testdata/misc
${PASSING}   ${MISCDIR}/normal.txt
${FAILING}   ${MISCDIR}/pass_and_fail.txt

*** Test Cases ***

Zero RC when all tests pass
    Run Tests and Verify RC  ${PASSING}                  rc=0

Zero RC when all critical tests pass
    Run Tests and Verify RC  --critical pass ${FAILING}  rc=0

Non-zero RC when critical tests fail
    Run Tests and Verify RC  ${FAILING}                  rc=1

Zero RC when all tests pass with --NoStatusRC
    Run Tests and Verify RC  --NoStatusRC ${PASSING}     rc=0

Zero RC when critical tests fail with --NoStatusRC
    Run Tests and Verify RC  --nostatusrc ${FAILING}     rc=0


*** Keywords ***
Run Tests and Verify RC
    [Arguments]  ${options & source}  ${rc}=
    ${returned}=  Run And Return Rc  ${ROBOT} --output NONE ${options & source}
    Should Be Equal As Integers  ${returned}  ${rc}
