*** Settings ***
Documentation   Test cases for failing suite setup and teardown. Some related tests are also found from test_suite_dir_with_suite_file.html
Force Tags      regression  jybot  pybot
Resource        atest_resource.txt

*** Variables ***
${1_pass_msg}  1 critical test, 1 passed, 0 failed\n 1 test total, 1 passed, 0 failed
${1_fail_msg}  \n\n1 critical test, 0 passed, 1 failed\n 1 test total, 0 passed, 1 failed
${2_fail_msg}  \n\n2 critical tests, 0 passed, 2 failed\n 2 tests total, 0 passed, 2 failed
${4_fail_msg}  \n\n4 critical tests, 0 passed, 4 failed\n 4 tests total, 0 passed, 4 failed
${5_fail_msg}  \n\n5 critical tests, 0 passed, 5 failed\n 5 tests total, 0 passed, 5 failed
${12_fail_msg}  \n\n12 critical tests, 0 passed, 12 failed\n 12 tests total, 0 passed, 12 failed
${also}  \n\nAlso teardown of the parent suite failed.
${teardown_file}  robot-suite-teardown-executed-0123.txt
${teardown_file}  robot-suite-teardown-executed-0123.txt

*** Test Cases ***
Passing Suite Setup
    ${suite} =  Run Tests And Get Suite  Passing Suite Setup
    Check Suite Status  ${suite}  PASS  ${1_pass_msg}
    Check Suite Contains Tests  ${suite}  Verify Suite Setup

Passing Suite Teardown
    [Setup]  Remove File  ${teardown_file}
    ${suite} =  Run Tests And Get Suite  Passing Suite Teardown
    Check Suite Status  ${suite}  PASS  ${1_pass_msg}
    Fail Unless File Exists  ${teardown_file}
    [Teardown]  Remove File  ${teardown_file}

Passing Suite Setup And Teardown
    [Setup]  Remove File  ${teardown_file}
    ${suite} =  Run Tests And Get Suite  Passing Suite Setup And Teardown
    Check Suite Status  ${suite}  PASS  ${1_pass_msg}
    Check Suite Contains Tests  ${suite}  Verify Suite Setup
    Fail Unless File Exists  ${teardown_file}
    [Teardown]  Remove File  ${teardown_file}

Failing Suite Setup
    [Documentation]  Checks that tests in the suite are found from it and marked failed. Also checks that suite teardown is executed.
    ${suite} =  Run Tests And Get Suite  Failing Suite Setup
    Check Suite Status  ${suite}  FAIL  Suite setup failed:\nExpected failure${2_fail_msg}
    Check Suite Contains Tests  ${suite}  Test 1  Test 2
    Equals  ${suite.setup.status}  FAIL
    Equals  ${suite.teardown.status}  PASS
    Ints Equal  ${suite.teardown.message_count}  1
    Check Log Message  ${suite.teardown.messages[0]}  Suite teardown executed
    Ints Equal  ${suite.teardown.keyword_count}  0

Erroring Suite Setup
    [Documentation]  Checks that tests in the suite are found from it and marked failed. Also checks that suite teardown is executed.
    ${suite} =  Run Tests And Get Suite  Erroring Suite Setup
    Check Suite Status  ${suite}  FAIL  Suite setup failed:\nNo keyword with name 'Non-Existing Keyword' found.${2_fail_msg}
    Check Suite Contains Tests  ${suite}  Test 1  Test 2
    Equals  ${suite.setup.status}  FAIL
    ${td} =  Set  ${suite.teardown}
    Equals  ${td.name}  My TD
    Equals  ${td.status}  PASS
    Ints Equal  ${td.message_count}  0
    Ints Equal  ${td.keyword_count}  2
    Ints Equal  ${td.keywords[0].message_count}  1
    Check Log Message  ${td.keywords[0].messages[0]}  Hello from suite teardown!
    Ints Equal  ${td.keywords[0].keyword_count}  0
    Equals  ${td.keywords[1].name}  BuiltIn.No Operation

Failing Suite Teardown When All Tests Pass
    ${suite} =  Run Tests And Get Suite  Failing Suite Teardown
    Check Suite Status  ${suite}  FAIL  Suite teardown failed:\nExpected failure${2_fail_msg}
    Check Suite Contains Tests  ${suite}  Test 1  Test 2
    Equals  ${suite.teardown.status}  FAIL

Failing Suite Teardown When Also Tests Fail
    ${suite} =  Run Tests And Get Suite  Failing Suite Teardown 2
    Check Suite Status  ${suite}  FAIL  Suite teardown failed:\nExpected failure${5_fail_msg}
    Check Suite Contains Tests  ${suite}  Test Passes  Test Fails  Setup Fails  Teardown Fails  Test and Teardown Fail
    Equals  ${suite.teardown.status}  FAIL

Erroring Suite Teardown
    ${suite} =  Run Tests And Get Suite  Erroring Suite Teardown
    Check Suite Status  ${suite}  FAIL  Suite teardown failed:\nNo keyword with name 'Non-Existing Keyword' found.${2_fail_msg}
    Check Suite Contains Tests  ${suite}  Test 1  Test 2
    Equals  ${suite.teardown.status}  FAIL

Failing Suite Setup And Teardown
    ${suite} =  Run Tests And Get Suite  Failing Suite Setup And Teardown
    Check Suite Status  ${suite}  FAIL  Suite setup failed:\n Setup failure\n\n Also suite teardown failed:\n Teardown failure${2_fail_msg}
    Check Suite Contains Tests  ${suite}  Test 1  Test 2
    Equals  ${suite.setup.status}  FAIL
    Equals  ${suite.teardown.status}  FAIL

Failing Higher Level Suite Teardown
    ${suite} =  Run Tests And Get Suite  Failing Suite Teardown Dir  \
    Check Suite Status  ${suite}  FAIL  Suite teardown failed:\nFailure in top level suite teardown${12_fail_msg}
    Check Suite Contains Tests  ${suite}  PTD Passing  PTD Failing  FTD Passing  FTD Failing  PTD PTD Passing  PTD PTD Failing
    ...  PTD FTD Passing  PTD FTD Failing  FTD PTD Passing  FTD PTD Failing  FTD FTD Passing  FTD FTD Failing
    ${ptd_suite} =  Get Test Suite  Passing Teardown Dir
    Check Suite Status  ${ptd_suite}  FAIL  Teardown of the parent suite failed.${4_fail_msg}
    Check Suite Contains Tests  ${ptd_suite}  PTD PTD Passing  PTD PTD Failing  PTD FTD Passing  PTD FTD Failing
    ${ptd_ptd_suite} =  Get Test Suite  Ptd Passing Teardown
    Check Suite Status  ${ptd_ptd_suite}  FAIL  Teardown of the parent suite failed.${2_fail_msg}
    Check Suite Contains Tests  ${ptd_ptd_suite}  PTD PTD Passing  PTD PTD Failing
    ${ptd_ftd_suite} =  Get Test Suite  Ptd Failing Teardown
    Check Suite Status  ${ptd_ftd_suite}  FAIL  Suite teardown failed:\nFailure in suite teardown${also}${2_fail_msg}
    Check Suite Contains Tests  ${ptd_ftd_suite}  PTD FTD Passing  PTD FTD Failing
    ${ftd_suite} =  Get Test Suite  Failing Teardown Dir
    Check Suite Status  ${ftd_suite}  FAIL  Suite teardown failed:\nFailure in sub suite teardown${also}${4_fail_msg}
    Check Suite Contains Tests  ${ftd_suite}  FTD PTD Passing  FTD PTD Failing  FTD FTD Passing  FTD FTD Failing
    ${ftd_ptd_suite} =  Get Test Suite  Ftd Passing Teardown
    Check Suite Status  ${ftd_ptd_suite}  FAIL  Teardown of the parent suite failed.${also}${2_fail_msg}
    Check Suite Contains Tests  ${ftd_ptd_suite}  FTD PTD Passing  FTD PTD Failing
    ${ftd_ftd_suite} =  Get Test Suite  Ftd Failing Teardown
    Check Suite Status  ${ftd_ftd_suite}  FAIL  Suite teardown failed:\nFailure in suite teardown${also}${also}${2_fail_msg}
    Check Suite Contains Tests  ${ftd_ftd_suite}  FTD FTD Passing  FTD FTD Failing

Failing Higher Level Suite Setup
    ${suite} =  Run Tests And Get Suite  failing_higher_level_suite_setup  \
    Check Suite Status  ${suite}  FAIL  Suite setup failed:\nExpected failure in higher level setup${2_fail_msg}
    ${subsuite}=  Get Test Suite  Suite 1
    Check Suite Status  ${subsuite}  FAIL  Setup of the parent suite failed.${1_fail_msg}
    ${subsuite}=  Get Test Suite  Suite 2
    Check Suite Status  ${subsuite}  FAIL  Setup of the parent suite failed.${1_fail_msg}


*** Keywords ***
Run Tests And Get Suite
    [Arguments]  ${name}  ${extension}=.html
    ${base} =  Set  ${name.replace(' ','_').lower()}
    Run Tests  ${EMPTY}  core${/}${base}${extension}
    [Return]  ${SUITE}

Check Suite Status
    [Arguments]  ${suite}  ${status}  ${message}
    Equals  ${suite.status}  ${status}  Wrong suite status
    Equals  ${suite.full_message}  ${message}  Wrong suite message

