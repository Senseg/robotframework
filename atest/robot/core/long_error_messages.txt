*** Settings ***
Documentation   Testing that possible long error messages are truncated
Suite Setup     Run Tests  ${EMPTY}  core${/}long_error_messages.txt
Force Tags      regression  jybot  pybot
Resource        ../../resources/resource.html

*** Variables ***
${4_SPACES}  \ \ \ \ \
${EXPLANATION}  \n ${4_SPACES}[ Message content over the limit has been removed. ]\n
${3DOTS}  \\.\\.\\.

*** Test Cases ***
Under The Limit
    Has Not Been Cut  50 x 1 Message Under The Limit
    Has Not Been Cut  100 x 1 Message Under The Limit
    Has Not Been Cut  50 x 10 Message Under The Limit
    Has Not Been Cut  150 X 9 Message Under The Limit
    Has Not Been Cut  77 x 19 Message Under The Limit

On The Limit
    Has Not Been Cut  20 x 20 Message On The Limit
    Has Not Been Cut  150 x 10 Message On The Limit
    Has Not Been Cut  1560 x 1 Message On The Limit

Over The Limit
    Has Been Cut  8 x 21 Message Over The Limit
    Has Been Cut  200 x 7 Message Over The Limit  601.*${3DOTS}  ${3DOTS}.*END
    Has Been Cut  1600 x 1 Message Over The Limit  .*${3DOTS}\\n  ${3DOTS}.*\\n

*** Keywords ***
Has Been Cut
    [Arguments]  ${testname}  ${eol_dots}=  ${bol_dots}=
    ${test} =  Get Test Case  ${testname}
    Should Contain  ${test.message}  ${EXPLANATION}
    Should Match Non Empty Regexp  ${test.message}  ${eol_dots}
    Should Match Non Empty Regexp  ${test.message}  ${bol_dots}

Should Match Non Empty Regexp
    [Arguments]  ${message}  ${pattern}
    Run Keyword If  '${pattern}' != ''  Should Match Regexp  ${message}  ${pattern}

Has Not Been Cut
    [Arguments]  ${testname}
    ${test} =  Get Test Case  ${testname}
    Should Not Contain  ${test.message}  Message content over

