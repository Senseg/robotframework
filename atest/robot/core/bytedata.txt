*** Settings ***
Documentation   In these tests keywords log, raise, and return messages containing ISO-8859-1 byte strings. Notice that they _do not_ use Unicode.
Suite Setup     Run Tests  ${EMPTY}  core/bytedata.txt
Force Tags      regression  pybot  jybot
Resource        atest_resource.txt
Variables       ../../testdata/core/expbytevalues.py

*** Test Cases ***
In Message
    [Documentation]  In the log message bytes are escaped.
    ${tc}=  Check Test Case  ${TESTNAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  ${exp_log_msg}

In Return Value
    [Documentation]  Return value is not altered by the framework and thus it contains the exact sama bytes that the keyword returned. When the return value is logged, the bytes are escaped.
    ${tc} =  Check Test Case  ${TESTNAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  \${retval} = ${exp_return_msg}

In Exception
    [Documentation]  In the error message bytes are escaped.
    ${tc} =  Check Test Case  ${TESTNAME}
    Check Log Message  ${tc.kws[0].msgs[0]}  ${exp_error_msg}  FAIL

In Exception In Setup
    [Documentation]  In the error message bytes are escaped.
    ${tc} =  Check Test Case  ${TESTNAME}
    Check Log Message  ${tc.setup.msgs[0]}  ${exp_error_msg}  FAIL

In Exception In Teardown
    [Documentation]  In the error message bytes are escaped.
    ${tc} =  Check Test Case  ${TESTNAME}
    Check Log Message  ${tc.teardown.msgs[0]}  ${exp_error_msg}  FAIL

