*** Settings ***
Documentation   Tests for invalid syntax in the test data. Non-existing variables in metadata are already tested in metadata.html and some other errors in resource_and_variable_imports.html
Suite Setup     My Setup
Force Tags      regression
Default Tags    jybot  pybot
Resource        atest_resource.txt

*** Test Cases ***
Valid Test
    ${test} =  Check Test Case  Valid Test
    Equals  ${test.doc}  This test should be executed and PASS

Library Imports
    Verify Error  Library setting requires a name  Setting
    Verify Error  Importing test library 'NonExistingLibrary' failed: ImportError:  Setting
    Check Test Case  Check Valid Library Import
    Verify Error  Replacing variables from setting 'Library' failed: Resolving variable '\${unicodeï¿½ï¿½ï¿½}' failed: Non-existing variable '\${unicode}'.  Setting

Resource Imports
    Verify Error  Resource file 'non-existing-resource.html' does not exist  Setting
    Verify Error  Resource setting requires a name  Setting
    Check Test Case  Check Valid Resource Import

Variable File Imports
    Verify Error  Variable file 'non-existing-variables.py' does not exist  Setting
    Verify Error  Variables setting requires a name  Setting
    Check Test Case  Check Valid Variable File Import

Invalid Setting
    Verify Error  Non-existing setting 'Invalid Setting'  Setting

Variables In Variable Table
    Verify Error  Setting variable 'Invalid Variable Name' failed: Invalid variable name 'Invalid Variable Name'.  Variable
    Check Test Case  Check Valid Variable In Variable Table

Non-Existing Variable In Test
    Check Test Case  Non-Existing Variable

Non-Existing Keyword
    ${test} =  Check Test Case  Non-Existing Keyword
    Starts  ${test.doc}  Note that doc and tags
    Fail Unless  ${test.tags} == ['example','tags']

Non-Existing Keyword In UK
    ${test} =  Check Test Case  Non-Existing Keyword In UK
    Starts  ${test.doc}  Note that doc and tags
    Fail Unless  ${test.tags} == ['more','tags']

Invalid Keyword Arguments
    ${test} =  Check Test Case  Invalid Keyword Arguments
    Starts  ${test.doc}  Note that doc and tags
    Fail Unless  ${test.tags} == [ ]

Invalid Keyword Arguments In UK
    ${test} =  Check Test Case  Invalid Keyword Arguments In UK
    Starts  ${test.doc}  Note that doc and tags
    Fail Unless  ${test.tags} == ['1', '2', '3']

Invalid Metadata In Test Case
    ${test} =  Check Test Case  Test Case With Invalid Metadata
    Equals  ${test.doc}  Invalid metadata is ignored with an error and this test will PASS
    Check Test Case  Check Test case With Invalid Metadata
    Verify Error  Invalid syntax in test case 'Test Case With Invalid Metadata': Non-existing setting 'Invalid In Test'.  Test Case

Invalid Metadata In User Keyword
    Check Test Case  User Keyword With Invalid Metadata
    Check Test Case  Check User Keyword With Invalid Meta
    Verify Error  Invalid syntax in keyword 'UK With Invalid Metadata': Non-existing setting 'Invalid In UK'.  Keyword

User Keyword Defined Multiple Times
    ${test} =  Check Test Case  User Keyword Defined Multiple Times
    Starts  ${test.doc}  Note that doc and tags
    Fail Unless  ${test.tags} == ['mytag']

*** Keywords ***
My Setup
    Run Tests  ${EMPTY}  core${/}invalid_syntax.html
    ${PATH} =  Join Path  ${CURDIR}..${/}..${/}..${/}testdata  core${/}invalid_syntax.html
    Set Suite Variable  $PATH

Verify Error
    [Arguments]  ${error}  ${table}
    Check Stderr Contains  [ ERROR ] Error in file '${PATH}'  in table '${table}': ${error}

