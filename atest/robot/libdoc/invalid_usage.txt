*** Settings ***
Force Tags       regression    pybot    jybot
Resource         libdoc_resource.txt
Test Template    Run libdoc and verify error


*** Variable ***
${USAGE TIP}    \nTry --help for usage information.\n

*** Test Cases ***

No arguments
    ${EMPTY}          Expected 2 arguments, got 0.

Too many arguments
    arg1 arg2 arg3    Expected 2 arguments, got 3.

Invalid option
    --invalid         option --invalid not recognized

Invalid format
    --format XXX BuiltIn ${OUTHTML}   Format must be either 'HTML' or 'XML', got 'XXX'.

Non-existing library
    NonExistingLib ${OUTHTML}   Importing test library 'NonExistingLib' failed: *

Invalid resource
    ${CURDIR}/invalid_usage.txt ${OUTHTML}
    ...   [ ERROR ] *: Non-existing setting 'Force Tags'.
    ...   [ ERROR ] *: Non-existing setting 'Test Template'.
    ...   Resource file '*' contains a test case table which is not allowed.


*** Keywords ***
Run libdoc and verify error
    [Arguments]    ${args}    @{error}
    Run libdoc and verify output    ${args}    @{error}    ${USAGE TIP}
