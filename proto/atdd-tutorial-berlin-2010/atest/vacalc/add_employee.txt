***Test Cases***

Adding employee
    Given no employee "Juha" exists in the system
    When "Juha" with start date 2010-01-01 is added
    Then message "Employee 'Juha' was added successfully." is shown
    and "Juha" exists in the employee listing
    and "Juha" with start date 2010-01-01 exists in the database

Adding employee with same name
    Given employee "Juha" with start date 2010-01-01 exists in the system
    When "Juha" with start date 2010-02-02 is added
    Then message "Employee 'Juha' already exists in the system." is shown
    and "Juha" with start date 2010-01-01 exists in the database

Adding Two Employees
    Given no employee exists in the system
    When "Pekka" with start date 2010-01-01 is added
    and "Janne" with start date 2010-01-01 is added
    Then "Janne" exists in the employee listing
    and "Janne" with start date 2010-01-01 exists in the database


*** Keywords ***
Given no employee "${name}" exists in the system
    Start Vacalc With No Employees

Given no employee exists in the system
    Start Vacalc With No Employees

"${name}" with start date ${date} is added
    Add Employee  ${name}  ${date}

Then message "${message}" is shown
    Status Message Should Be  ${message}

"${name}" exists in the employee listing
    Employee Should Be Selected  ${name}

and "${name}" with start date ${date} exists in the database
    Employee exists in the database  ${name}  ${date}

Given employee "${name}" with start date ${date} exists in the system
    Start Vacalc With No Employees
    Add Employee  ${name}  ${date}


***Settings***
Force Tags   Sprint-1
Resource  resource.txt
Test Teardown  Close Vacalc