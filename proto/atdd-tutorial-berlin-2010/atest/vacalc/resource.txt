*** Settings ***
Library  SwingLibrary
Library  OperatingSystem

*** Variables ***
${DATABASE}  ${CURDIR}${/}test_db.txt

*** Keywords ***

Start Vacalc
    ${ctx} =  Run Keyword And Ignore Error  Get Current Context
    Set Environment Variable  VACALC_DB  ${DATABASE}
    Start Application  org.robotframework.vacalc.VacalcRunner
    Select Window  Vacation Calculator

Start Vacalc with No Employees
    Clear Database
    Start Vacalc

Close Vacalc
    Close Window  Vacation Calculator

Clear Database
    Remove File  ${DATABASE}

Add Employee
    [Arguments]  ${name}  ${date}
    Push Button  new_employee_button
    Type Into Text Field  name_input  ${name}
    Type Into Text Field  start_input  ${date}
    Push Button  save_button

Status Message Should Be
    [Arguments]  ${message}
    Label Text Should Be  status_label  ${message}

Employee Should Be Selected
    [Arguments]  ${name}
    ${selected item}=  Get Selected Value From List  employee_list
    Should Be Equal  ${name}  ${selected item}

Employee exists in the database
    [Arguments]  ${name}  ${date}
    ${db} =  Get File  ${DATABASE}
    Should Contain  ${db}  ${name},${date}

Employee should not exist in the database
    [Arguments]  ${name}
    ${db} =  Get File  ${DATABASE}
    Should Not Contain  ${db}  ${name}

