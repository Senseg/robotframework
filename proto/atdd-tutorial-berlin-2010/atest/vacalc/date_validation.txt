*** Settings ***
Force Tags     Sprint-0  Regression
Resource       resource.txt
Test Template  Validate start date


*** Variables ***
${USER}  Mr. Roboto


*** Test Cases ***

Valid date
    2010-09-29  Employee '${USER}' was added successfully.
    2010-9-9    Employee '${USER}' was added successfully.

Invalid date format
    invalid     Invalid time string.
    2010-09-xx  Invalid time string.
    2010-09     Invalid time string.
    2010.09.09  Invalid time string.
    20101010    Invalid time string.
    29.9.2010   Invalid time string.

Empty time string
    ${EMPTY}    No start time given.

Invalid month
    2010-42-01  Month must be in 1..12.
    2010-13-01  Month must be in 1..12.
    2010-00-01  Month must be in 1..12.

Invalid day
    2010-1-00   Day must be in 1..31.
    2010-1-32   Day must be in 1..31.
    2010-1-100  Day must be in 1..31.
    2010-4-31   Day must be in 1..30.
    2010-2-30   Day must be in 1..28.
    2012-2-30   Day must be in 1..29.


*** Keywords ***

Validate start date
    [Arguments]  ${date}  ${message}
    Start Vacalc with no employees
    Add employee  ${USER}  ${date}
    Status message should be  ${message}
